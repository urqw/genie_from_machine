:0
invkill
perkill

urq_type=1
style_backcolor="#222"

:Старт
Style_dos_TextColor =14
if urq_type=0 then pln                       	GENIE EX MACHINE#/$                      	"Джинн из машины"#/$             	Автор - Евгений Туголуков ака Korwin, #/$             	Автор картинки Евгений Бычков ака Евг#/$ else proc introhtml
if читал_пролог=0 then btn Пролог,Пролог 
if читал_пролог>0 then btn Начало,Начать игру 
btn история_квеста,История этого квеста
/* btn attention,Внимание, совместимость! */
btn Выход_из_квеста,Выход из квеста
End

:common
style_dos_cursorcolor=112
if count_Виктор0>0 then r=#r+vi$&if rad>10 then rad=rad-1 else rad=rad+rnd5
end

:use_inv_сохраниться
save #%current_loc$
pln
pln Игра сохранена.
pln
end

:attention
pln Внимание! Квест тестировался на интерпретаторе urq_dos.exe(wcl) от 30.11.2004 года, рекомендую играть на нем же или использовать AkURQ 1.1 Fast Edition. Не поддерживаются интерпретатор URQ 1.4 от RipOs, BestURQ, AkURQ 1.25 бета.  Поскольку в игре потребуется вводить русские слова, то рекомендуется использовать именно AkURQ 1.1 или urq_dos(wcl) или urq_dos.exe c предварительно запущенным русификатором (например keyrus.com).
btn Старт,Вернуться в меню
end

:introhtml
pln <center>
_<hr>
_<font size=6 color="yellow">
_Гений из машины
_</font>
_<hr>
_</center>
_<div align="center"><img src="gexm.jpg" class="foto" alt="ИНТРО"></div>
_<br>
_<p style="text-align: right; display: block;">
_<font size=4 color="orange">
_автор идеи и текста Евгений Туголуков ака Korwin<br>
_автор картинки - заставки Евгений Бычков ака Евг
_</font>
_</p>
end

:prologhtml
pln 
_<p style="text-align: justify; display: block;">
_    Свершилось! В начале XXI века великое противостояние совпало с готовностью человечества отправить космолет к Марсу! Международный проект поддержали 49 государств! Сначала была построена научная орбитальная станция "Содружество", уже на которой собирался ядерный космолет "Беллона". Строительство продолжалось уже почти два года - ведь надо было не только разработать систему жизнеобеспечения, импульсно-ядерный двигатель, корпус, легкую, но надежную радиационную защиту, тысячи приборов, датчиков, процессоров, программное обеспечение ко всем этим системам, - но и увязать все эти сложнейшие механизмы, к тому же разработанные в разных странах, - в единое гармоничное и безотказно работающее целое. Проблема надежности стала настоящим кошмаром для разработчиков. Почти готовый корабль постоянно подвергался самым разнообразным испытаниям: точную копию его корпуса обстреливали из электромагнитной пушки, имитируя попадание метеорита, в корабле искусственно то повышали, то понижали температуру, наблюдая, как отреагирует на это издевательство система поддержания микроклимата, ядерный реактор выводили на критические режимы энергопотребления... Кто это делал? Разумеется не только члены экипажа - большая их часть в это время была слишком занята на Земле. Для этой работы был выделен большой коллектив космических инженеров-испытателей. Одним из них был Виктор Санин, специалист по импульсным двигателям и реактору. Он и его очаровательная подруга по имени Рита и станут главными героями нашей игры. Причем играть Вам придется по очереди за обоих героев...
_</p>
end

:Пролог
читал_пролог=1
D=12
H=13
T=3
if urq_type=0 then pln Свершилось! В начале XXI века великое противостояние совпало с готовностью человечества отправить космолет к Марсу! Международный проект поддержали 49 государств! Сначала была построена научная орбитальная станция "Содружество", уже на которой собирался ядерный космолет "Беллона". Строительство продолжалось уже почти два года - ведь надо было не только разработать систему жизнеобеспечения, импульсно-ядерный двигатель, корпус, легкую, но надежную радиационную защиту, тысячи приборов, датчиков, процессоров, программное обеспечение ко всем этим системам, - но и увязать все эти сложнейшие механизмы, к тому же разработанные в разных странах, - в единое гармоничное и безотказно работающее целое. Проблема надежности стала настоящим кошмаром для разработчиков. Почти готовый корабль постоянно подвергался самым разнообразным испытаниям: точную копию его корпуса обстреливали из электромагнитной пушки, имитируя попадание метеорита, в корабле искусственно то повышали, то понижали температуру, наблюдая, как отреагирует на это издевательство система поддержания микроклимата, ядерный реактор выводили на критические режимы энергопотребления... Кто это делал? Разумеется не только члены экипажа - большая их часть в это время была слишком занята на Земле. Для этой работы был выделен большой коллектив космических инженеров-испытателей. Одним из них был Виктор Санин, специалист по импульсным двигателям и реактору. Он и его очаровательная подруга по имени Рита и станут главными героями нашей игры. Причем играть Вам придется по очереди за обоих героев... else proc prologhtml
btn Старт,В меню
End


:historyhtml
pln <p align="left">
_<font size=4 color="darkgreen">
_Первоначально этот квест делался на конкурс ЛОКА 2005 года с задумкой соответствовать сразу ТРЕМ заявленным темам.
_</font>
_<br>
_<hr>
_УСЛОВИЯ КОНКУРСА ЛОКА 2005: <br><br>
_Главное правило: игра должна соответствовать как минимум одной из указанных тем:<br>
_<ol>
_<li>Тема бесполезности разума. Уточнение: в основу сюжета должна лечь ситуация, при которой разум оказывается бессилен.
_<li>Ожившая вещь. Уточнение: в игре должен присутствовать сабж (ожившее существо, объект или явление) в любом виде - от искусственного интеллекта, до говорящего бревна. Роль сабжа не должна быть эпизодической.
_<li>Тема помощника Героя. Уточнение: в игре должен присутствовать эпический Герой и самый обычный человек - помощник. Действие должно происходить вокруг подвигов Героя, но повествование и игра идёт от лица Помощника.
_</ol>
_<br>
_<hr>
_Но, -увы, - на конкурс квест не успел! И был отложен в долгий ящик. Тем не менее, не пропадать же из-за этого хорошему квесту! Тем более, после появления нового интерпретатора URQL - AkURQ от Акелы, я смог сделать его если не лучше, то немного красивее. Надеюсь, он доставит Вам, уважаемый игрок удовольствие... Второй раз этот квест делался на ЛОКу 2006 по теме "Общее дело". Тоже не успел. Спасибо Виктору - сжалился и заставил меня-таки его доделать! Хотя бы часть первую... 
_</p>
end

:история_квеста
if urq_type=0 then pln Первоначально этот квест делался на конкурс ЛОКА 2005 года с задумкой соответствовать сразу ТРЕМ заявленным темам.#/$УСЛОВИЯ КОНКУРСА ЛОКА 2005:#/$Главное правило: игра должна соответствовать как минимум одной из указанных тем:#/$- Тема бесполезности разума. Уточнение: в основу сюжета должна лечь ситуация, при которой разум оказывается бессилен.#/$- Ожившая вещь. Уточнение: в игре должен присутствовать сабж (ожившее существо, объект или явление) в любом виде - от искусственного интеллекта, до говорящего бревна. Роль сабжа не должна быть эпизодической.#/$- Тема помощника Героя. Уточнение: в игре должен присутствовать эпический Герой и самый обычный человек - помощник. Действие должно происходить вокруг подвигов Героя, но повествование и игра идёт от лица Помощника.#/$#/$Но, -увы, - на конкурс квест не успел! И был отложен в долгий ящик. Тем не менее, не пропадать же из-за этого хорошему квесту! Тем более, после появления нового интерпретатора URQL - AkURQ от Акелы, я смог сделать его если не лучше, то немного красивее. Надеюсь, он доставит Вам, уважаемый игрок удовольствие... Второй раз этот квест делался на ЛОКу 2006 по теме "Общее дело". Тоже не успел. Спасибо Виктору, Дженни, Goraph'у - вы заставили меня его доделать! else proc historyhtml
btn старт,В меню
End


:Выход_из_квеста
if urq_type=0 then pln Ну в самом деле, зачем Вам сейчас выходить? Если сильно приспичит - нажмите ESC на клавиатуре. Дважды. А сейчас предлагаю вернуться в стартовое окно игры. else Pln  Ну в самом деле, зачем Вам сейчас выходить? Если сильно приспичит - нажмите крестик в правом верхнем углу программы или CTRL-X на клавиатуре. А сейчас предлагаю вернуться в стартовое окно игры.
btn старт,В меню
End


:Начало
Style_dos_TextColor =13
pln "БЕЛЛОНА": ВИКТОР
pln
Style_dos_TextColor =14
M=D-T
instr endmin=ы
if M=1 then instr endmin=а
if M>4 or M=0 then instr endmin=
pln Рубка космолета имеет полусферическую форму. Между двух, "лежащих" на полу кресел - люк в главный отсек. Над головой - вогнутый круглый экран, на котором медленно вращается звездное небо. Часть звезд закрыта белым корпусом космической станции "Содружество". Виктор лежит, пристегнувшись к левому креслу, и смотрит на маленький дисплей под главным экраном. Правое кресло пусто. На дисплее мигают цифры электронных часов. До начала испытания остается #M$ минут#%endmin$.
if M>1 then btn Об_испытании,Подумать об испытании реактора
if M>3 then btn осмотреть_космолет_до_испытаний,Встать и осмотреть весь космолет
if M>1 then btn осмотреть_пульт_до_испытаний,Осмотреть пульт управления
if M>1 then if осм_себя<2 then btn осмотреть_себя_до_испытаний,Поглядеть в зеркало
if M>1 then if осм_себя>0 and ritamax=0 then btn осмотреть_дисплей,Посмотреть на дисплей
btn ждать_начала_испытаний,Ждать начала испытаний
end

:Об_испытании
pln Суть испытания проста: насос подает рабочее вещество в разогретый реактор##59$ тот, повинуясь управляющим командам бортового компьютера направляет раскаленные газы одновременно в разгонные и тормозные дюзы так, чтобы их тяга взаимно уравновесила друг друга и корабль не сдвинулся с места. Датчик ускорения – акселерометр, - фиксирует все отклонения от нуля и подает управляющий сигнал в компьютер. Компьютер изменяет тягу таким образом, чтобы погасить возникшее ускорение. Задача – плавно довести мощность реактора до максимума, продержать его в этом режиме 30 секунд и так же плавно погасить тягу. Одновременно проверяется как надежность работы реактора и управляющих элементов на критических режимах, так и способность компьютера идеально управлять ядерным двигателем, не допуская отклонений от заданного программой ускорения (в данном случае - нулевого). Аналогичное испытание на Земле, - с использованием ракетной тележки, установленной на рельсах, прошло нормально.
T=T+1
btn начало,Дальше 
End

:осмотреть_космолет_до_испытаний
T=T+1
if M<2 then pln Уже поздно! Осталось слишком мало времени на обход космолета. Не стоит сейчас покидать кресло.&goto Начало
Pln Виктор прикинул время, - решил, что успеет, - и расстегнул ремни. Оттолкнулся от кресла, аккуратно открыл люк и вылетел из рубки в кают-компанию. 
btn кают-компания0,Дальше 
End

:осмотреть_пульт_до_испытаний
Pln  Виктор еще раз, - на всякий случай, - контролирует показания приборов: температура в реакторе - норма, - холостой ход, давление в системе охлаждения - норма, температура охладителя - норма, напряжение в бортовой сети - норма, ускорение - ноль... прочие приборы - норма.
T=T+1
btn начало,Дальше 
End

:осмотреть_себя_до_испытаний
P  Виктор смотрит в зеркало на дисплее и делает вывод: Я вполне симпатичный молодой человек! Пусть невысок, но крепкого сложения, с обаятельной широкой улыбкой. 
if осм_себя=1 then pln И все-таки непонятно, что Рита во мне такого увидела? Таких как я - много, есть и посильнее, и пообаятельнее, и куда богаче... Или её привлекла романтика космоса? Так какая тут сейчас романтика? Работа как работа, только невесомость. И ведь в космос часто не попадешь - в основном всё на Земле происходит. Странно.
осм_себя=осм_себя+1
T=T+1
btn начало,Дальше 
End

:осмотреть_дисплей
Pln  На дисплее - центр управления полетом (ЦУП). Поскольку до испытания еще осталось несколько минут, камера периодически переключается на разные планы и видно то сосредоточенное лицо диспетчера, то ряды операторов уткнувшихся в экраны - они сейчас проверяют и контролируют нормальную работу всех систем, обеспечивающих полет корабля и станции... если совсем точно - контролируют работу компьютеров, обеспечивающих нормальный полет корабля и станции. Иногда камера показывает "галерку" - застекленный балкончик для зрителей. Туда иногда приводят экскурсии, там сидят провожающие космонавтов друзья и родственники. Внезапно Виктор замечает на "галерке" Риту. Девушка прямо глядит на него - точнее, в камеру, - и беззвучно шепчет что-то.
T=T+1
btn махать,Помахать Рите
End

:махать
Pln Виктор поднимает руку м плавно машет девушке - в невесомости быстро отвыкаешь от резких движений. Обрадованная Рита широко улыбается и посылает в камеру воздушный поцелуй! Потом что-то говорит - но беззвучно, так как микрофон к галерке не подключен, - и грозит Виктору пальчиком. Тут камера снова переключается на руководителя полетом.
T=T+1
btn начало,Дальше 
End

:кают-компания0
Pln Кают-компания это цилиндрическое помещение из которого ведут несколько люков. Пара круглых иллюминаторов сейчас прикрыта крышками, пространство между ними украшают пейзажи Земли, большой экран обеспечивает отличный обзор тем, кто разместится в семи креслах на противоположной стене.
T=T+1
btn начало,В рубку
btn некогда,В оранжерею
btn некогда,В приборный отсек
btn некогда,В жилой модуль
btn некогда,К реактору
btn некогда,В шлюз
End

:некогда
Pln Нет, сейчас совершенно некогда шататься по кораблю!
btn начало,Вернуться в рубку.
End

:ждать_начала_испытаний
T=D
Pln Прошло несколько мгновений. Руководитель полетом кивнул и сказал просто: "Начали..."
pln Диспетчер стал запрашивать готовность разных систем - всё оказалось в порядке. Тогда он скомандовал: "Запустить программу испытаний!"
btn испытание,Нажать кнопку ввода на клавиатуре
End

:испытание
Pln Нажав кнопку, Виктор запустил программу. На дисплее поползли вверх индикаторы мощности реактора, мерно загудел главный насос... Корабль дрогнул, отсоединившись от стыковочного узла – для чистоты эксперимента нужно было двигаться независимо от станции. Удалившись от станции на сотню метров, "Беллона" развернулась так, чтобы выхлопы дюз были направлены не на станцию и не на Землю и выпустила из себя одновременно: вперед и назад, - два голубых "кинжала" пламени. 
pln Испытание проходило не совсем гладко - если со стороны казалось, что корабль стоит неподвижно, то внутри Виктор собственной спиной ощущал мягкие толчки - то вперед, то назад. Ничего подобного не было в испытаниях аналогичного реактора на Земле...
pln Внезапно весь космолет резко дернулся, взвыли насосы, и тормозное пламя - погасло! Страшная тяжесть обрушилась на Виктора - в багровой пелене он успел лишь увидеть как акселерометр показал ускорение в 10 G! 
btn ЦУП,Потерять сознание
End

:ЦУП
Style_dos_TextColor=13
pln ЦЕНТР УПРАВЛЕНИЯ ПОЛЕТАМИ: РИТА
pln
Style_dos_TextColor=14
Style_BackColor=0
p Рита Денисова стояла перед камерой и махала рукой Виктору. Конечно, перед испытанием он был слишком занят, но все же она надеялась, что он ее увидит. 
if count_махать>0 then p И чудо произошло! - он действительно улыбнулся и помахал в ответ с большого экрана, висящего за стеклянной стеной. Потом любимое лицо снова стало серьезным - началось испытание. else p Но любимое лицо оставалось слишком серьезным, и Рита опечалилась. Началось испытание.
pln  Раздавались вопросы диспетчера, ответы операторов и Виктора, затем на главном экране ЦУП стало видно как "Беллона" отделилась от станции и стала медленно удаляться. Потом корабль выпустил параболическую чашку антенны, направив ее прямо на Землю. Через несколько мгновений на носу и корме космолёта "расцвели" голубые цветы пламени. Сначала крошечные, они всё удлинялись и удлинялись, пламя стало уже длиннее самого космолёта! И вдруг весь экран озарила бело-синяя вспышка, "Беллона" рванулась с места и в миг исчезла с экрана - лишь кувыркалась в космосе обломившаяся чашка антенны...
btn визг,Завизжать
btn ЦУП2,Зажать рот руками
end

:визг
P  Рита истерично завизжала, закрыв от ужаса глаза!!! Дернулись сотрудники ЦУПа, поворачивая на неё головы, и сердито сверкнул глазами руководитель полётов. 

:больница
p К Рите вскоре подбежали врач с медсестрой и, что-то быстро и ласково говоря, вкололи успокоительное. Потом ещё... Потом ещё - в больнице.#$
pln Полгода спустя Рита вышла из клиники. Прошлая любовь к Виктору, кошмарное испытание в космосе - всё это как-то стерлось из памяти.#$

:жизнь_продолжается
pln Всё прошло. Еще через полтора года Рита вышла замуж. Только иногда, увидев яркую обложку "желтого" журнала с дурацкой надписью "Одиноки ли мы во Вселенной?" она вздрагивала. И еще - она терпеть не могла глядеть ночью на звезды...
btn поражение,...
End

:поражение
if urq_type=0 then pln Увы, Вам не удалось вновь соединить влюблённых!&btn 0,Начать сначала, начать с нуля?&End

pln <p>
_<center>
_<hr>
_<font size=7 color="yellow">
_GAME OVER<br>
_</font>
_<font size=7 color="orange">
_КОНЕЦ
_</font>
_<hr>
_<div align="center"><img src="gexm.jpg" class="foto" alt="ИНТРО"></div>
_<font size=5 color="red">
_Увы, Вам не удалось вновь соединить влюблённых!
_</font>
_</center>
_</p>
btn 0,Начать сначала, начать с нуля?
End

:ЦУП2
inv_visible true
pln Зажав рот рукой, Рита сдержала крик. Ноги ослабели от ужаса и она опустилась в кресло, не отрываясь глядя в операционный зал центра управления полетами. Суматоха постепенно улеглась - волевой голос руководителя быстро навёл порядок. Пошли доклады - цифры, градусы, расчеты траектории. Потом всё чаще стали слышаться фразы "ограниченный запас рабочего вещества", "разгон окончен", "гиперболическая скорость". Рита знала от Санина, что такое "гиперболическая скорость". Это такая скорость относительно небесного тела, которая позволяет преодолеть силу притяжения и навсегда уйти в космическое пространство. НАВСЕГДА!
pln 
pln В это время руководитель полетов объявил, что все руководители подразделений собираются на совещание. Люди встали и пошли в соседний зал. Диспетчер попросил всех посторонних покинуть Центр управления полетами. Посетители двинулись к выходу.
btn стадо,Выйти вместе с посетителями
btn спрятаться,Спрятаться за стульями
btn совещание,Встать и пойти в соседний зал на совещание
end

:спрятаться
pln Рита сделала вид, что уронила ручку, нагнулась и спряталась между стульями... Вскоре зал почти опустел, и тут к ней подошел строгий охранник, помог подобрать ручку и очень вежливо, но твёрдо проводил ее до двери.
btn стадо,Выйти за двери, сохраняя достоинство
btn визг,Завизжать: "Вы не имеете права! Я останусь здесь!"
end

:стадо
pln Рита вышла и двери ЦУПа закрылись за ее спиной... В глазах стояли слезы - она уже интуитивно понимала, что больше никогда-никогда не увидит Виктора.
btn вернись_мгновение,Вернуться в центр управления полетами
btn идти_домой,Идти домой
btn слезы,Заплакать
end

:вернись_мгновение
pln Рита побежала обратно и стала стучать в двери, но ей не открывали, холодно заявив, что постороннии сейчас в Центр управления полетами не допускаются.
btn идти_домой,Идти домой
btn слезы,Заплакать
end

:слезы
pln Слезы потекли в два ручья. Рита шла по улице и плакала навзрыд... Какой-то молодой человек остановил ее, вытер слезы платком, проводил до дому. Молодой человек был очень внимателен и умел слушать, к тому же, похоже Рита понравилась ему своей беззащитностью... Они стали встречаться всё чаще... Сначала они говорили о трагической судьбе Виктора, но заметив, что эта тема причиняет девушке боль, молодой человек менял тему и говорил уже о ней, о её душе, о будущем, о красоте природы...
goto жизнь_продолжается

:идти_домой
pln Девушка шла домой и яркое солнце казалось ей чёрным, а свежий весенний ветерок - ледяным. Горе подступало к горлу всё ближе и ближе, как комок, который мешал дышать.
btn слезы,Разрыдаться
btn вернись_мгновение,Побежать назад и колотить кулаками в двери ЦУПа
end

:совещание
pln В небольшой зал для совещаний собралось около двух десятков человек: конструкторы, инженеры, техники, программисты, космонавты. Они стали рассаживаться по стульям вокруг овального стола. Рита заметила неподалеку знакомое лицо - космонавта - бортинженера Сергея Спыхальского, друга Виктора. Что делать?
btn стул,Сесть на ближайшее сиденье
btn стеночка,Остаться стоять у стеночки
btn знакомый,Сесть рядом с Сергеем
end

:стул
pln Рита быстро садится на первый попавшийся стул.  Уже почти все сели. Тут ее замечает Сергей Спыхальский и жестом зовет ее к себе.
btn пересаживание,Пересесть поближе к Спыхальскому 
btn на_месте,Помотать головой и остаться на месте
end

:пересаживание
pln Рита поднимается именно в тот момент, когда все уже сели! Разумеется, все взгляды оказываются сосредоточенными на ней! Особенно удивленным выглядит Руководитель полетов.
goto удаление

:стеночка
pln Все рассаживаются по местам и только Рита остается стоять, прислонившись спиной к стенке, чем сразу обращает на себя внимание Руководителя управления полетами. 

:удаление
pln - Вы, собственно, кто? - спрашивает он, и, не дожидаясь ответа, говорит по селектору: - Удалите посторонних с совещания!
pln Через несколько секунд появляется охранник и бережно, за ручку, выводит девушку из зала совещаний, а потом и к выходу из центра управления полётами...
btn стадо,Выйти за двери, сохраняя достоинство
btn визг,Завизжать: "Вы не имеете права! Я останусь здесь!"
end

:знакомый
pln Сергей удивленно смотрит на девушку и спрашивает шёпотом: "Ты здесь как оказалась? Это же закрытое совещание!" На его шёпот оборачивается сосед слева с суровым лицом и значком службы безопасности на лацкане пиджака. Что ответить Сергею?
btn на_выход,- Помоги, пожалуйста, мне очень надо здесь быть!
btn на_выход,- Потому что я невеста Виктора! А ты его друг!
btn на_месте,- Я же психолог! Потому и пригласили...
btn на_выход,- Я не знала, что оно закрытое!
end

:на_выход
pln Сергей кивает, но "безопасник" чуть нагибается к Рите и таким же шёпотом говорит ей: "Очень сожалею, но прошу Вас немедленно выйти из зала совещаний. Это действительно закрытое совещание. Вы не имеете права здесь находиться."
btn стадо,Встать и выйти
end

:на_месте
pln Спыхальский пожимает плечами и успокаивается. Совещание начинается кратким вступлением Руководителя полётов:
pln - Товарищи! Все в курсе, что на "Беллоне" произошла авария, поставившая под угрозу срыва весь проект полета к Марсу. На повестке дня три вопроса - и он записал их на доску:
pln 1. Что именно произошло на космолете и почему?
pln 2. Что будет происходить с космолетом дальше?
pln 3. Что будем делать мы с Вами для спасения космонавта и проекта в целом?
pln После недолгого молчания слово взял лысоватый толстяк - руководитель службы астрономических наблюдений:
pln - По нашим данным "Беллона" внезапно, на 4-й секунде испытаний ядерного двигателя приобрела ускорение,  растущее от 1 до 25 G(же) и, в течение нескольких минут развив скорость порядка 18 километров в секунду, стала удаляться прочь от Земли. Данная скорость, к сожалению, направленная по вектору движения Земли по орбите является гиперболической по отношению не только к Земле, но и к Солнцу. Следовательно, через некоторое время, - порядка нескольких месяцев, - космолет неминуемо покинет пределы Солнечной системы. У меня всё.
btn визг,Закричать от ужаса
btn совещание2,Сдержаться и слушать дальше
end

:совещание2
pln Дальше стал говорить представитель КБ: "Мы собрали данные телеметрии по реактору и пришли к следующему выводу: на первых 3-х секундах испытания ядерный двигатель работал нормально, если не считать незначительной, в пределах ожидаемого допуска, разбалансировки между тягой главной и тормозной установок. На четвертой секунде реактор получил приказание центрального компьютера повысить мощность тяги главной двигательной установки и ускорение стало расти. Параллельно росту ускорения шли приказы компьютера об увеличении тяги. Мы можем предположить наличие в программе компьютера сбоя, приведшего к возникновению положительной обратной связи между показаниями акселерометра и тягой главного двигателя, вместо отрицательной, которая должна была бы компенсировать непредвиденное ускорение."
pln Рита внимательно вслушивалась в речь, пересыпанную техническими терминами, пытаясь понять главное: "Что грозит Виктору? Жив ли он сейчас?"
pln К счастью, вопрос о судьбе космонавта задал сам руководитель полетов.
pln Представитель космической медицины, - женщина средних лет с короткой стрижкой, - высказалась, что "при перегрузке в 25 G она гарантирует потерю сознания и зрения у космонавта на время ускорения и предрекает серьезные нарушения в работе сердечно-сосудистой системы, вероятно, несовместимые с жизнью..."
btn визг,Вцепиться докторше в волосы
btn совещание3,Сдержаться и слушать дальше
end

:совещание3
pln - Погодите хоронить космонавтов! Виктор Санин на центрифуге выдерживал, не теряя сознания 12 G, полагаю, в экстренной ситуации мог и больше, а Вы говорите, "неминуемое". "несовместимое с жизнью"! - возмутился Сергей. - Кстати, а чем Вы объясните прекращение работы реактора? Ведь в настоящее время двигатель космолета молчит?
pln Докторша, смущенная, села. Но Сергею ответил представитель КБ: "Количество рабочего вещества на космолете было очень ограничено - фактически, только на проведение нескольких испытаний. Так что скорее всего реактор выключился самостоятельно после исчерпания запаса рабочего вещества, которое одновременно является охладителем. Во избежание взрыва реактора в него встроено автоматическое отключение на критических режимах работы, независимое от главного компьютера."
pln - Что показывают данные телеметрии сейчас? - спросил руководитель полета.
pln - Ничего. - ответил представитель телеметристов. - При высоком ускорении антенна была сломана и мы уже не улавливаем сигналы космолета, даже если они еще и поступают. Связь с "Беллоной" потеряна. Мы получаем информацию о космолете сейчас только от астрономов.
btn совещание4,Слушать дальше
end

:совещание4
pln - В каком направлении сейчас движется космолет? Где он будет находиться через сутки, пять, десять? Через месяц? Вы рассчитали орбиту? - раздраженно спросил руководитель полета.
pln - Точных данных пока нет, но по предварительным - орбита гиперболическая, вне плоскости эклиптики. - ответил лысый астроном.
pln - Плохо. - вздохнул Сергей.
pln - Каков ресурс системы жизнеобеспечения? Сколько сможет жить космонавт без помощи с Земли? - руководитель повернулся к женщине-медику. Та ненадолго задумалась, проглядывая свои записи в папке, потом сказала:
pln - Система жизнеобеспечения уже готова к полёту на Марс, следовательно, там загружено кислорода, воды и продуктов питания на три года... но в расчёте на 9 человек. Санину - если он жив, - чисто арифметически хватит на 27 лет. Конечно, нельзя забывать об ограниченном сроке годности многих продуктов... но думаю, в настоящее время это не проблема.
pln Кто-то присвистнул удивленно. 
pln - Энергии у него достаточно? - спросил руководитель полета.
pln - Реактор рассчитан на 4 года работы - с двукратным резервом, получается на 6-8 лет хватит. - ответил энергетик. Подумав, добавил: - есть еще резервные солнечные батареи, но при удалении за орбиту Марса они станут бесполезны.
pln - Так, - невесело пошутил кто-то, - с таким запасом лети хоть к звездам!
pln - Долетишь - за сто тысяч лет! - отозвался астроном мрачно.
pln Все замолчали.
btn совещание5,Мысленно помолиться за Виктора
end

:совещание5
pln Помолчав, руководитель полётов подвел итоги:#/$ "Итак, после аварии космолет удаляется от Земли на 18 километров в секунду, не располагая никаким запасом рабочего вещества, чтобы его не просто вернуть, но и хотя бы замедлить. Неизвестно, жив ли космонавт Виктор Санин и связи с ним мы не имеем, причем в силу увеличения расстояния вероятность связаться с космолетом уменьшается с каждой секундой. Чем вызвана авария мы можем только предполагать... Запас системы жизнеобеспечения позволяет космонавту существовать автономно в течение шести лет минимум, но догнать его возможности нет. Я правильно изложил ситуацию?"
pln Специалисты недружно закивали головами.
pln Руководитель полетов обвел всех тяжелым взглядом и спросил: "Какие есть предложения по спасению космонавта?"
pln Все угрюмо молчали... Наконец один из участников совещания - специалист по связям с общественностью, - тихо сказал: "Санина спасти невозможно. Надо готовить сообщение для прессы."
btn визг,Влепить гаду пощечину
btn предложение_психолога,Предложить организовать "мозговой штурм".
btn записка,Написать записку Сергею с предложением организовать "мозговой штурм".
btn молчание,Молчать и ждать что будет дальше.
end

:молчание
pln Все молчали еще пару минут. Потом Руководитель полётов сказал: "Что ж, раз других мнений нет, подготовьте это сообщение. Сообщите о героической гибели Виктора Санина во время испытаний." 
pln Женщина-медик побледнела, астроном перекрестился, Спыхальский сжал тяжелые кулаки, очкастый специалист по связям с общественностью начал что-то быстро набивать в своем крошечном ноутбуке.
btn обругать_руководителя,Вскочить и обозвать руководителя полетов идиотом
btn предложение_психолога,Предложить организовать "мозговой штурм".
btn записка,Написать записку Сергею с предложением организовать "мозговой штурм".
btn молчание2,Молчать и ждать что будет дальше.
end

:молчание2
pln - Все свободны, совещание окончено. - печально сказал постаревший на глазах руководитель полётов. Люди стали расходиться. Никто не хотел говорить друг с другом. Рита, еле сдерживая слезы, выскочила на улицу и побежала... Солнце казалось ей черным диском, небо обжигало ледяным холодом, люди провожали ее взглядами...
btn слезы,Вытереть слезы рукавом
end

:обругать_руководителя
pln Рита не выдержала напряжения - слезы уже душили ее, - вскочила и закричала: - Вы идиот! Более того - Вы - предатель! Вы все предатели! - и разрыдалась. Вокруг собрались люди.
btn больница,Попросить стакан воды
end

:предложение_психолога
pln Рита встала - все взоры обратились на нее и выйдя из-за стола, обратилась к собравшимся: 
pln - Я знаю, как вам всем тяжело и трудно, знаю, что в создавшемся положении кажется, что выхода нет. Но на самом деле - выход есть, только мы не можем его увидеть сразу... Я предлагаю
pln Ее прервал руководитель полётов: "Вы собственно, кто?" И, не дослушав ответ, повернулся к секретарю:
pln - Почему на совещание присутствуют посторонние?
pln Через пару секунд появились двое крепких молодых людей, которые без разговоров вывели Риту из зала. Потом ее допрашивал строгий "безопасник", но убедившись в ее невиновности, взял с нее подписку о невыезде из города и отпустил... Вышла из здания ЦУП она только поздно вечером.
btn жизнь_продолжается,Пойти домой
end

:записка
pln Рита быстро написала короткую записку Сергею: "Предложи организовать мозговой штурм по проблеме. Помнишь как это делается?"
pln Сергей прочел, кивнул и встал:
pln - У меня есть предложение. - он сделал паузу. Все посмотрели на него.
pln - Я отлично знаю Виктора Санина. Это один из наших лучших специалистов. Я уверен, что сейчас он жив и делает всё возможное, чтобы спасти корабль. Космонавтов учат, что безвыходных предложений не бывает, правильно?
pln - К чему вы клоните? - спросил спец по связям с общественностью.
pln - Если мы не видим решения, это не значит что его нет! Предлагаю сейчас же организовать "мозговой штурм" по спасению человека. Согласны?
pln Руководитель полётов вздохнул, обвел взглядом присутствующих и кивнул за всех, дескать, организуй.
pln Сергей стер с доски все вопросы и записал один: "Спасение Виктора Санина?"
pln Потом повернулся к собравшимся и сказал: "Теперь каждый должен высказать одно или несколько предложений по спасению. Идеи могут быть любыми - самыми фантастическими и даже бредовыми! Разбирать их мы будем потом. Критиковать нельзя, развивать идеи других - можно. Поехали!"
btn штурм,Записывать идеи на лист бумаги
end

:штурм
pln Рита стала записывать. Сначала идей было немного, потом народ "разогрелся" и количество бреда резко возросло:
pln - Использовать для связи радиотелескоп РАТАН-600 - он уловит даже самые слабые сигнали с корабля!
pln - Передать сигнал Виктору боевым лазером.
pln - Ага, точно. Если стандатный передатчик неисправен, пусть Санин использует зеркало - будем ловить отраженный сигнал!
pln - А зеркало можно сделать из зеркальной пленки для оранжерей!
pln - Сделать солнечный парус и лететь до Земли на нём - раз на Беллоне есть отражающая пленка!
pln - В качестве антенны можно использовать ту же пленку, а в качестве каркаса - запасные трубки охлаждения от реактора!
pln - Использовать питьевую воду в качестве рабочего вещества! Ее на борту несколько тонн!
pln - Развернуться, используя притяжение Юпитера! - Нет, развернуться вокруг Солнца!
pln - Сделать прямоточный двигатель на звездном газе!
pln - Построить второй корабль - вдвое быстрее "Беллоны" и сгонять туда-обратно!
pln - Помолиться всей планетой!.. Нет, лучше обратиться к Люциферу!
pln После этого предложения все на миг замолчали и руководитель полетов укоризненно покачал головой, потом сам предложил:
pln - Сделать спасательную шлюпку и лететь на ней к Земле!
pln - Расплавить часть корпуса и использовать его в качестве рабочего вещества!
pln - Обеспечить связь и передать на Землю генетический код Санина, Потом сделать клон!
pln Обсуждение прервалось... Все помрачнели.
pln Потом руководитель полётов критическим взглядом обвел доску с записями и мрачно сказал: "Ну как, что мы можем выбрать как руководство к действию?"
btn обсуждение,Спрятать записи в сумочку
btn список_наизусть,Выучить список наизусть
end

:список_наизусть
pln Рита чуть прикрыла глаза и стала учить список идей наизусть. Это немного отвлекало ее от обсуждения, но она понимала, что самое важное она все равно не пропустит...
inv+ Идеи
goto обсуждение

:обсуждение
inv+ Список
pln - Мне кажется, - высказался астроном, - идеи насчет РАТАН-600 и лазерной связи неплохие!
pln - Пожалуй, да! - согласился руководитель. Это мы сделать можем. Давайте исходить из того, что связь мы с Виктором Саниным наладим. Что дальше?
pln Тем временем Рита спрятала листок в сумочку... И тут она заметила подозрительный взгляд сотрудника службы безопасности.
btn чертик,Рисовать на другом листке чёртика и звёзды
btn портрет,Нарисовать на листке  портрет Виктора
end

:портрет
pln Рита неплохо умеет рисовать - она два года ходила в художественную школу, да и в профессии психолога умение рисовать красиво ценится. Несколькими штрихами она рисует портрет Виктора. Тем временем обсуждение продолжается.
inv+ Портрет
btn решения,Слушать
end

:чертик
pln Рита неплохо умеет рисовать - она два года ходила в художественную школу, да и в профессии психолога умение рисовать красиво ценится. Несколькими штрихами она рисует злобного чертика, сидящего на Луне в окружении звезд. Тем временем обсуждение продолжается.
inv+ Рисунок
btn решения,Слушать
end

:решения
pln - Трубки охлаждения лучше не трогать - нечем будет чинить реактор в случае поломки, - заметил энергетик. Идею вычеркнули.
pln - Питьевой воды там тонн пять - чтобы затормозить стодвадцатитонный корабль на 18 км/секунду этого слишком мало! Зачеркиваем.
pln - Юпитер и прочие планеты-гиганты находятся в плоскости эклиптики, а космолет сейчас летит вне ее, то есть в сторону от них! Нереально! - сказал астроном.
pln - Вокруг солнца разворот идет на эллиптической скорости, в крайнем случае на параболической, а у нас гипербола! Вот было бы поменьше... можно было бы жить...
pln В этот момент Риту кто-то трогает за плечо.
btn взяли,Обернуться
end

:взяли
pln Рита оборачивается и видит, что ее трогает за плечо очень высокий человек в сером костюме. Он говорит ей: "Пройдемте за мной... "
btn сопротивление,Возмутиться
btn пройти,Встать и пойти
end

:сопротивление
pln - Вы не имеете права! - говорит Рита громко.
pln - Я - имею. Я начальник службы безопасности Александр Шумов. - твёрдо говорит высокий. - А вот вы не имеете права здесь находиться. 
pln Рита сбрасывает руку безопасника с плеча.
pln - Обратите внимание, - говорит высокий окружающим, - агентка оказала сопротивление при задержании.
pln Все смотрят на Риту с изумлением. Даже Сергей.
btn пройти,Встать и пойти
end

:пройти
Style_dos_TextColor=13
pln ЦЕНТР УПРАВЛЕНИЯ ПОЛЕТАМИ, КАБИНЕТ №302: РИТА
pln
Style_dos_TextColor=14
Pln Вскоре Рита оказывается в кабинете начальника службы безопасности ЦУП Александра Шумова. Он закрывает дверь на ключ, усаживается за стол и долго молча смотрит на девушку. 
pause 2000
pln Потом говорит неожиданно громко: "Кто такая? Документы? Как здесь оказалась?"
btn допрос1,Назваться и показать документы
end
/*;--------Инвентарь--------------*/
:use_Портрет
pln Это рисунок ручкой - портрет Виктора. Лохматые волосы, курносый нос и печальные глаза получились очень выразительно.
end

:use_Рисунок
pln Это рисунок ручкой - злобный чертенок, сидящий на Луне в окружении звезд.
end

:use_Идеи
pln Вот что запомнила Рита с "мозгового штурма":
pln 1. Зеркальная плёнка - солнечный парус.
pln 2. Радиотелескоп РАТАН-600
pln 3. сигнал боевым лазером
pln 4. зеркало - отраженный сигнал
pln 5. антенна из запасных трубок охлаждения от реактора
pln 6. питьевая вода - рабочее вещество
pln 7. развернуться у Юпитера
pln 8. развернуться вокруг Солнца
pln 9. прямоточный двигатель на звездном газе
pln 10. второй корабль - вдвое быстрее "Беллоны"
pln 11. Молитва
pln 12. Люцифер
pln 13. спасательная шлюпка
pln 14. расплавить корпус
pln 15. клон
end

:use_Список
pln Здесь записи Риты с мозгового штурма:
pln - Использовать для связи радиотелескоп РАТАН-600 - он уловит даже самые слабые сигнали с корабля!
pln - Передать сигнал Виктору боевым лазером.
pln - Пусть В. сделает зеркало - ловить отраженный сигнал!
pln - Сделать солнечный парус и лететь до Земли на нём - на Беллоне есть отражающая пленка!
pln - Антенна: можно использовать запасные трубки охлаждения от реактора!
pln - Использовать питьевую воду в качестве рабочего вещества!
pln - Развернуться, используя притяжение Юпитера!
pln - Развернуться вокруг Солнца!
pln - Сделать прямоточный двигатель на звездном газе!
pln - Построить второй корабль - вдвое быстрее "Беллоны" и сгонять туда-обратно!
pln - Помолиться всей планетой!.. Нет, лучше обратиться к Люциферу!
pln - Сделать спасательную шлюпку и лететь на ней к Земле!
pln - Расплавить часть корпуса и использовать его в качестве рабочего вещества!
pln - Обеспечить связь и передать на Землю генетический код Санина, Потом сделать клон!
end

:допрос1
pln - Рита Денисова, психолог. Я невеста космонавта Виктора Санина. Вот мой паспорт, вот пропуск.
pln - Оп-па! Пропуск-то у вас, гражданочка, только для зоны А! Вы у нас получается, нелегал! - говорит Шумов весело, но при этом в глазах его отражается неизмеримая скука, словно он тысячу раз на дню задерживал шпионов проникших на секретное совещание. Затем он протягивает руку:#/$- Дайте сумочку!
/*;btn отдать_сумочку,Отдать сумочку*/
btn схватить_сумочку,Схватить сумочку и заявить, что без адвоката разговаривать Вы не будете.
end

:схватить_сумочку
pln Высокий смотрит на Риту с изумлением: Какого адвоката? Ты не понимаешь, девочка, во что ты вляпалась... - он говорит уверенно и даже с какой-то жалостью:
pln - А вляпалась ты в главу 29 Уголовного кодекса Российской Федерации, то есть ПРЕСТУПЛЕНИЯ ПРОТИВ ОСНОВ КОНСТИТУЦИОННОГО СТРОЯ И БЕЗОПАСНОСТИ ГОСУДАРСТВА, а именно статья 275, ГОСУДАРСТВЕННАЯ ИЗМЕНА, то есть шпионаж, выдача государственной тайны либо иное оказание помощи иностранному государству, иностранной организации или их представителям в проведении враждебной деятельности в ущерб внешней безопасности Российской Федерации, совершенная гражданином Российской Федерации, - наказывается лишением свободы на срок от двенадцати до двадцати лет... И если ты будешь оказывать мне сейчас хоть какое-то сопротивление, я позабочусь, чтобы это было не "от двенадцати", а "до двадцати", поняла? 
btn отдать_сумочку,Отдать сумочку
end

:отдать_сумочку
p - Так, - говорит высокий - и безжалостно вываливает всё содержимое сумочки на стол. Потом достает какой-то миниатюрный приборчик и проводит им над грудой вещей, состоящей из зеркальца, пудреницы, губнушки, гребешка, календарика и прочей мелочи. Жестом предлагает встать и поднять руки. Рита встает, помня предупреждение насчёт "до двадцати" и поднимает руки, но не как арестант, а чуть-чуть фривольно потягиваясь, с таким видом, словно ей этого самого захотелось. Шумов хмурится, потом тем же приборчиком проводит вдоль тела Риты, обводя все ее контуры, но не касаясь. Говорит: "Садитесь." - и возвращается к вещам, лежащим на столе.
if Список then p  С торжествующим видом извлекает из кучи мелочей листок с записями.#/$ - По старинке работаешь, девочка? Вот улика-то!&inv- Список
if Рисунок then p  Обнаружив рисунок с чертиком, сверяет его со списком и вдруг становится очень серьезным и злым, наклоняется над самым лицом Риты и шёпотом спрашивает:#/$- Что тебе известно о проекте "Люцифер"?#/$&btn ор,Возмутиться
if Портрет then p  Обнаружив портрет Виктора, достает из папки фотографию, сверяет... и поджимает губы.#/$
p - Так что вот за эту вот бумажку, ждёт  Вас, гражданка Денисова Маргарита Николаевна, по-минимуму д в е н а д ц а т ь  л е т  с т р о г о г о  режима, то есть без свиданий и права переписки. И отпахаешь ТЫ эти двенадцать лет полностью, без всяких амнистий, в специальной колонии, где будешь на практике знакомиться с психологией уголовниц-рецидивисток и убийц. Кстати, ТЫ случайно, не лесбиянка? Нет? Ну так готовься, будешь! А если выйдешь оттуда, повторяю, ЕСЛИ ВЫЙДЕШЬ, то скорее всего уже с туберкулёзом в легких, остриженными волосами и личиком в частую морщинку. 
pln Он замолк, записывая что-то в свои бумаги. Задумался. Встал. Обошёл Риту сзади и сказал ей на ухо, вполголоса: Честно говоря, Маргарита Владимировна, мне Вас как, то даже жаль немного, такую молодую и красивую туда отдавать... 
invkill Портрет
invkill Рисунок
pln  И вдруг неожиданно закричал в самое ухо: "Говори, на кого работаешь, тварь!"
btn плач,Заплакать
btn логика,Давить на логику
end

:плач
pln Рита от неожиданности начинает рыдать, - ну что поделаешь, такой напряженный день,а она всё же эмоциональная девушка, - и, почувствовав в ее всхлипах слабость, Шумов гладит ее по плечу, и говорит: "Понимаю Вас, Вам трудно. Расскажите всё как есть, я готов выслушать, и Вам станет легче.
btn о-любви,Рассказать ему всё с самого начала
end

:ор
pln - Не смейте так со мной разговаривать! Не знаю я никакого проекта "Люцифер", я просто чёртика нарисовала, потому что мучилась, потому что Виктор в беде и никто ему помочь не может. А список записала, потому что психолог и знаю технику проведения мозговых штурмов - здесь очень важно ничего из предложенного не упустить, любая идея, самая бредовая на первый взгляд может подтолкнуть к решению. А Вы орёте на меня, оскорбляете, как будто Вы - НКВДшник времен Сталина! Палач!
pln - А между прочим, в отношении иностранных шпионов нелюбимое Вами НКВД действовало весьма эффективно. Так что не пытайтесь меня оскорбить этим сравнением, оно мне льстит. Так Вы будете говорить, или мне записать в протокол что Вы отказались сотрудничать и тем самым смягчить Вашу участь?
btn плач,Заплакать
btn логика,Попытаться защититься
end

:логика
pln - Товарищ полковник, Вы всерьез надеетесь, что я - иностранный шпион? Напрасно, версия не оправдается, хоть всю мою жизнь перекопайте! Не была я никогда за границей, и знакомых там у меня нет никого, и даже компьютера дома нет - пользуюсь тем, что в нашей школе. Я обычный школьный психолог, а здесь оказалась потому, что я невеста Виктора Санина и пропуск мне вполне официально выдан за подписью Вашего заместителя майора Дорофеева. А на собрание прошла по вполне понятной причине - потому что очень беспокоюсь за своего жениха и хочу что-то сделать для его спасения или хотя бы быть в курсе того, что для его спасения что-то делается. И максимум, что мне реально грозит - это порицание за проникновение в зал совещаний. А вот Вам или вашим сотрудникам кое-что грозит за то, что недосмотрели и пропустили постороннего человека. Я правильно излагаю ситуацию?
pln Полковник гневно посмотрел на Риту и сказал:
pln - Ты мне не строй из себя правоведа, не в суде находишься, и все доводы твои - тьфу, плюнуть и растереть! Не беспокойся, проверим мы твою биографию, и если окажется, что ты переписывалась с какими-нибудь зарубежными психологами, или общалась с иностранными журналистами из числа тех, кто сюда приезжает, вопрос о твоей виновности по другому встанет. Где и как ты познакомилась с космонавтом Виктором Саниным?
btn возмутиться,- С какой стати я должна это рассказывать?
btn о-любви,- Хорошо, расскажу, только Вы успокойтесь и не брызгайте на меня слюной!
end

:возмутиться
pln С какой стати? А с какой стати Я вообще должен с Вами разговаривать? Моя задача какая? Обеспечить информационную безопасность, то есть отсутствие утечки информации. Зачем мне лишняя головная боль, допрос этот после окончания рабочего дня? Вот провожу тебя, девочка, за пределы центра и закрою в стерильном гермобоксе для перехода на борт "Бурана", а ключ потеряю. "Буран" уж 20 лет, как на постаменте стоит, но гермобокс в наличие у нас имеется, законсервированный. И обрати внимание, - за 20 лет в него никто не заглядывал. Сомневаюсь, что заглянут в следующие 20. А когда-таки откроют, я буду на пенсии. Кстати, ты в курсе, что в стерильной атмосфере труп не гниёт, а мумифицируется?
pln ...
pln А я тут время свое трачу, пытаюсь шанс спастись для тебя найти? Так где и когда ты познакомилась с Саниным?
btn о-любви,Рассказать...
end


:о-любви
pln - Мы познакомились с Виктором год назад, в парке культуры и отдыха - я гуляла с подругой, а он на турнике "солнце" крутил, много раз, и мы с подругой поспорили, космонавт он или нет. Подошли, спросили дорогу на танцплощадку, а он сначала спрыгнул и стал объяснять, а потом мы глазами встретились и он вызвался проводить. По дороге и познакомились. Потом танцевали. Потом стали встречаться... Рита замолкает, а Шумов, который всё это время делал пометки у себя в бумагах, поднимает голову и спрашивает:
pln - Значит, Вы признаете, что инициатива знакомства исходила от Вас?
pln Рита теряется от такого вопроса, но потом говорит:
btn допрос2да,- Да.
btn допрос2нет,- Нет.
btn допрос2да,- Ну и что? Теперь вроде бы девушкам проявлять инициативу не аморально?
end

:допрос2нет
pln - Что значит нет? Сами же сказали, что подошли к нему?
pln - Ну да, мы подошли, но потом он уже сам с нами пошёл!
pln - То есть вы начали разговор?
btn допрос2да,- Да
end

:допрос2да
pln - Значит, признаете! - кивает головой Шумов. - Как зовут подругу, кем работает, где живёт?
btn ксана,- Оксана Росомахова, оператор в вычислительном центре, живет по Московской 5.
btn нескажу,- Не скажу я Вам про нее ничего.
end

:ксана
pln - Вычислительный центр? Хмм... проверим, проверим... Кто добился подписания пропуска в центр управления полетом?
btn допрос3,Сергей Спыхальский, друг Виктора.
end

:нескажу
pln - Опять запираетесь?
pln - Узнавайте сами. Мне подруга не простит, если Вы из-за меня ее будете допрашивать.
pln - Эх, Вы, девочка! Вам бы сейчас самой срок скостить, а Вы о других думаете! Ладно, вернемся к этому вопросу позже - он делает пометку и продолжает допрос:
pln - Кто добился подписания пропуска в центр управления полетом?
btn допрос3,Сергей Спыхальский, друг Виктора.
end

:Допрос3
pln ...
pln Допрос продолжался четыре часа. Полковник спрашивал что делала Рита в центре управления полетами - поминутно, много раз, снова и снова. Предложил воды, заметив, что девушке трудно говорить, но в туалет не выпускал. Садюга! Уже стемнело за окном, когда он сказал совершенно измочаленной девушке: 
pln - Ладно. Предположим, что я Вам поверил, и Вы невиновны. Распишитесь вот здесь, здесь и здесь. Это - протокол допроса, это подписка о невыезде, это Ваше обещание хранить в тайне всё, что узнали в центре управления полетами. Воздержитесь от разговоров на эту тему со всеми, в том числе с родными.
btn подписка, Прочитать документы и подписать
btn подписка, Подписать, не читая
end

:подписка
pln - Ну вот, Вы теперь предупреждены об ответственности за разглашение государственной тайны. Поезжайте домой, и помните, что сейчас в течение некоторого времени, Вы будете под надзором. До свидания!
pln Рита собрала свои вещи (кроме списка) и покинула кабинет начальника службы безопасности. На выходе из центра ее ждала машина, из которой ей навстречу выскочил Сергей Спыхальский. Он был неразговорчив и приложив палец к губам посоветовал ей молчать. Вскоре они оказались у ее дома. Усталая Рита вышла из машины. Сергей - тоже.
btn вопрос_у_машины,Задать вопрос Сергею
btn дверь_подъезда,Молча открыть дверь подъезда
end

:вопрос_у_машины
pln - Ты хочешь поговорить? - спросила Рита, глядя в скрытое поднятым воротником лицо Спыхальского.
pln Сергей кивает головой и громко отвечает: "Нет, слишком устал сегодня. Покурю и поеду домой." - с этими словами он открывает и снова захлопывает дверь машины.
btn дверь_подъезда,Молча открыть дверь подъезда
end

:дверь_подъезда
Style_dos_textcolor=13
pln КВАРТИРА РИТЫ ДЕНИСОВОЙ: РИТА
pln
Style_dos_textcolor=14
pln Рита поворачивается спиной к Сергею, который ведет себя так странно, открывает дверь... Сергей входит следом, двигаясь мягко и бесшумно, как громадный кот, вставший на задние лапы. Вскоре они входят в квартиру. Рита включает свет. Сергей показывает рукой на ванную, крутит пальцами, как будто открывая воду.
btn ванная,Войти в ванную и включить воду
end

:ванная
pln Девушка открывает воду в ванной, Сергей входит в одних носках, бегло осматривает комнату, находит халат и протягивает Рите. Жестами показывает, чтобы она переоделась в халат. Рита удивлённо поднимает брови. Сергей отворачивается к окну.
btn переодеться,Сбросить костюм и одеть халат
btn выгнать_Сергея,Выгнать Сергея вон
end

:выгнать_Сергея
pln Рита говорит сердито: "Иди вон, Сергей! Мне надоели твои подмигивания и домогательства! Как ты можешь! Я устала и хочу остаться одна!"
pln Сергей разочарованно говорит: "Я думал... а впрочем, уже неважно..." и уходит. Рита остается одна, сначала плачет, потом тяжелый сон одолевает ее... На следующий день она пыталась связаться с Сергеем, но никто не брал трубку телефона. В Центр Управления полетами ее тоже больше не пускали. А через две недели по радио объявили о гибели Виктора Санина.
btn жизнь_продолжается,Крепиться
end

:переодеться
pln Сергей дождался, пока Рита переоделась и дотронулась до его плеча, обернулся, осмотрел сброшенную одежду и показал Рите крохотную булавку с толстой головкой, поднял вверх палец, отнес "жучок" в ванную и разместил его неподалеку от бегущей из крана воды. Потом сказал шепотом: "Нас могут прослушивать даже через окно. Буду краток. Ты совершила глупость, проникнув на совещание, но спасибо за идею организовать мозговой штурм. Благодаря этому, я думаю, найден способ связаться с Виктором. Я уверен, что он жив."
btn спросить_результат,Спросить о результатах совещания
end

:спросить_результат
pln Рита задумалась и спросила:#/$- А чем кончилось совещание?
pln Сергей махнул рукой: "Ничем! Кроме связи, не было ни одной путной идеи."
pln - Не может быть! Там ведь было столько идей! Какие, собственно там были против них аргументы?
pln - Какую идею ты конкретно имеешь в виду?
if not Идеи then pln Жалко, что Шумов отнял у меня список. Теперь и не вспомнить точно. Ну, я всё же попробую.
btn вспомнитьвсе,Вспомнить все идеи
end

:вспомнитьвсе
if Идеи then pln Тренированная память психолога легко выдает список идей.&proc use_Идеи else p После допроса у безопасника вопросы звучавшие на совещании как-то вылетели из головы у Риты.#$
pln Про что спросить у Сергея? Он схватывает мысль с одного ключевого слова.
pln
style_dos_textcolor=13
instr idea=.
input idea
style_dos_textcolor=14
pln
if idea=="*солнечный*" or idea=="*парус*" then pln Спыхальский  говорит: Солнечный парус? Столько зеркальной плёнки нет на борту "Беллоны", к тому же будет невыгодный угол, поскольку надо не разогнаться от солнца, а затормозить. Чушь, в общем, полнейшая.
if idea=="*шлюпка*" or idea=="*капсула*" then pln Спыхальский  говорит: Сделать силами одного человека из спускаемого аппарата шлюпку для полета в космосе - самая реальная из идей. Запаса воздуха в спускаемом аппарате-капсуле, если в расчете на одного человека, хватит на три недели, ну на месяц, если выкинуть все приборы. Проблема в том, что двигатель капсулы - химический и может в лучшем случае обеспечить разгон примерно до 7 км/секунду (он же был рассчитан только на посадку и взлет с Марса). Поэтому, после применения этого двигателя Сергей по прежнему будет удаляться от Земли, правда уже по эллиптической орбите, то есть за пределы солнечной системы он не уйдет. Но за месяц мы никак не вытащим его с расстояния в 500 миллионов километров!#/$То есть, получается, что в большом корабле можно жить долго, но его нельзя остановить, а маленькую шлюпку можно затормозить, но в ней нельзя жить долго? - спросила Рита, заломив в отчаянии руки.#/$- Да. Этот вариант - последний, он отнимает у Виктора несколько лет жизни в обмен на возможность остаться в солнечной системе...
if idea=="*разворот*" or idea=="*Юпитер*" then pln Спыхальский  говорит: развернуться у Юпитера - красивая идея, но к сожалению, Беллона летит совсем не в ту сторону.
if idea=="*клон*" then pln Спыхальский  говорит: клонировать Виктора можно, но кто вернет клону память? И чем это изменит судьбу его самого?
if idea=="*Солнц*" then pln Спыхальский  говорит: Разворот вокруг Солнца возможен, если скорость Беллоны будет меньше третьей космической, а именно меньше 16.3 км/секунду относительно Земли. Причем для того, чтобы этот разворот не занял десятилетия, желательно не 16, а где-то 12-13.
if idea=="*другой*" or idea=="*корабль*" then pln Спыхальский  говорит: другой корабль уже есть, но он не готов, а когда он будет готов, вряд ли сможет догнать "Беллону" за приемлемое время - там получается что-то около 9 лет.
if idea=="*молит*" then pln Спыхальский  говорит: Молиться можно, и наверное нужно, вот только я не помню, чтобы молитва помогала в реальной трудной проблеме без приложения своих рук. Если бы для жизни нужна была только молитва, Бог не дал бы нам рук.
if idea=="*звездный*" or idea=="*газ*" then pln Спыхальский  говорит: Звездный газ слишком разрежен, да и нет средств на "Беллоне", чтобы его ловить. Бред.
if idea=="*расплавит*" or idea=="*корпус*" then pln Спыхальский  говорит: Здравая мысль - с точки зрения изобретательства, но технически очень сложная, вряд ли Виктор сможет ее осуществить. Это ж надо разрезать корабль на части##59$ их расплавить в каком-то котле, подать в реактор по сравнительно узкой трубке... Я, конечно, в отличие от Витьки не двигателист, но представляю, насколько сложно это сделать... К тому же эти 120 тонн - вовсе не сплошной металл, а металлокерамика, пластик, двести тысяч километров проводов, приборы... И многие из них жизненно необходимы. Думаю, это полный бред.
if idea=="*вода*" or idea=="*воду*" then pln Спыхальский  говорит: Воды на борту "Беллоны" 5 тонн, корабль весит 125, при скорости истечения рабочего вещества 5 км/секунду скорость корабля можно изменить примерно на 200 м/секунду. Не спасет!
if idea=="*лазер*" or idea=="*зеркало*" then pln - Да, идея с лазером и зеркалом самая реальная и она, как я и говорил уже делается. Только устойчивой связь будет еще максимум месяц-полтора... Потом космолёт уйдет слишком далеко.
if idea=="*трубки*" or idea=="*антенн*" then pln - Трубки охлаждения для изготовления антенны лучше не трогать - нечем будет чинить реактор в случае поломки, - говорит Сергей. - Идею, вроде бы еще при тебе вычеркнули. Лазерная связь - надежнее и проще.
if idea=="*ЛЮЦИФЕР*" then goto о_люцифере1
btn вспомнитьвсе,Попробовать вспомнить ещё
btn термины,Спросить про непонятные слова
btn бесполезно,Это бесполезно, всё равно не вспомнить
end

:термины
pln - Да, еще пара вопросов Сергей, по терминам:
btn эклиптика,- Что такое эклиптика?
btn рабвещ,- Что такое рабочее вещество?
btn гипербола,- Что такое гиперболическая скорость?
btn вспомнитьвсе,Попробовать вспомнить ещё
end

:эклиптика
pln - Что такое эклиптика?
pln - Плоскость эклиптики - это плоскость, в которой вращаются все планеты солнечной системы. При межпланетных перелётах стараются выйти из плоскости эклиптики, чтобы снизить риск столкновения с метеоритом или космической пылью. С другой стороны, если б Виктор летел в плоскости эклиптики, был бы шанс подкорректировать курс в сторону планеты-гиганта, например Сатурна - он довольно близок по направлению к вектору скорости, - и развернуться около него, при минимальных затратах топлива. Но - не судьба. 
btn термины,- Еще вопрос...
end

:рабвещ
pln - Что такое рабочее вещество?
pln - "Беллона" имеет двухкаскадный импульсно-ядерный двигатель. Активное вещество - плутоний, реактор на быстрых нейтронах дает одновременно электрическую и тепловую энергию, Рабочее вещество - обыкновенная вода, сначала охлаждает термопарный генератор, затем попадает в раскаленную рабочую зону реактора, испаряется, превращается в пар с температурой около 1000 градусов, затем идёт во второй каскад, где под действием электрического импульс-разряда и гидромагнитного генератора превращается в холодную плазму с температурой порядка 6000 градусов и вылетает из двигателя. Сопла двигателя защищаются от перегрева магнитным полем и охлаждаются тем же рабочим веществом. Сейчас, по нашим расчетам, рабочего вещества на борту "Беллоны" нет. А вот активного вещества - плутония, полный запас.
btn термины,- Понятно...
end
 
:гипербола
pln - Что такое гиперболическая скорость? Поподробнее, пожалуйста. Это очень опасно, да?
pln - Нет. Тут тебе надо немного космической механики рассказать. Все тела во Вселенной движутся подчиняясь фундаментальным законам, которые известны тебе в упрощенном виде как три закона Ньютона. То есть если на тело не действуют внешние силы, оно движется по инерции, равномерно и прямолинейно. В космосе внешние силы - это притяжение Солнца и планет. Ну есть еще давление света, электромагнитные поля и т.п. но как правило ими можно пренебреь. Если скорость свободно движущегося тела меньше первой космической - 7,8 км в секунду, то находясь вблизи Земли оно обязательно на нее упадет, поскольку будет двигаться по эллиптической орбите, перигей которой находится под поверхностью Земли. Если скорость тела равна первой космической и направлена по касательной к поверхности Земли, оно будет двигаться почти по круговой орбите. Если больше 8 и меньше 11,2 км/секунду - по эллиптической, апогей которой будет уходить всё дальше и дальше от Земли. 
btn эллипс,- Мудрёно, но вроде понятно. А если больше 11-ти?
end

:эллипс
pln Если больше 11,2 - тело приобретет по отношению к Земле гиперболическую скорость и уйдет от Земли совсем, но останется где-то в Солнечной системе, только дальше от Солнца. 
pln - А у "Беллоны" сколько?
pln Сергей помрачнел. У "Беллоны" сейчас около 20. В сложении со скоростью Земли вокруг Солнца получается около 50 километров в секунду по касательной к орбите Земли - этого хватит, чтобы оторваться от Солнца и улететь из Солнечной системы. Ой, да не плачь ты, Рита!
btn термины,Вытереть слёзы.
end

:бесполезно
pln  ...Ну да ладно, - говорит Сергей, - поздно уже, утро вечера мудренее. Спокойной ночи!
pln Он уходит, Рита мстительно сует всю свою одежду вместе с булавкой-жучком в стиральную машину-автомат и включает ее, а потом ложится спать. Заснуть удается только после двух таблеток снотворного...
pln Через двое суток Спыхальский позвонил ей и сообщил, что Виктор вышел на связь, более того, ему удалось обнаружить неисправность. К сожалению, в центр ее пока не пустят. Но сеанс связи был долгий и подробный, так что всё стало понятно. Главное - Виктор жив!
btn онеисправности,Спросить, что за неисправность...
end

:онеисправности
pln - Да представляешь, там какая-то ерунда получилась, - отвечает Сергей, начиная подробно рассказывать, что и как делал Виктор после аварии.
btn Виктор0,Слушать про Виктора...
end

:о_люцифере1
pln - Откуда ты знаешь о проекте ЛЮЦИФЕР? - изумился космонавт.
pln - Неважно. Объясни, о чём там речь?
pln - Я не имею права! - воскликнул Сергей в сердцах.
btn endo,- Даже ради друга? 
end

:endo
pln Хорошо, я скажу тебе. Но сам я знаю мало, пусть расскажет мой друг-программист. Мы встретимся послезавтра, - отговорился Сергей и поспешно ретировался в прихожую.
btn бесполезно,Проводить Сергея
end


:Виктор0
invkill Идеи
rad=129
vi=19.2376
r=245121
zapas=5
Style_dos_textcolor=13
pln "БЕЛЛОНА": ВИКТОР
pln
Style_dos_textcolor=14
:Виктор1
pln ...Виктор очнулся. Открыл глаза. Испугался - перед глазами стояло багровое зарево. Потом разглядел приборную доску и успокоился. Понял, что в глазах кровь от лопнувших кровеносных сосудиков. Пошевелился. Перегрузка упала до 2G, но каждое движение, прикосновение к коже отзывалось болью. Похоже, всё тело - сплошной синяк. На дисплее бортового компьютера горит тревожная надпись: "Внимание! Недопустимая перегрузка! Основной бак рабочего вещества пуст! Резервный запас рабочего вещества #zapas$%! Перегрев реактора! Опасно!"
btn стопдвигатель,Выключить подачу рабочего вещества.
btn стопреактор,Применить аварийные стоп-стержни и погасить реактор!
btn думатьпрореактор,Подумать.
end

:стопреактор
pln Виктор протягивает руку к красной кнопке сброса аварийных стоп-стержней и нажимает ее! Вскоре перегрузка резко ослабевает, освещение мигает, переключаясь на аккумуляторы, и на дисплее меняется надпись: "Реактор аварийно остановлен! Основной бак рабочего вещества пуст! Резервный запас рабочего вещества #zapas$%! Истечение рабочего вещества продолжается! Непредвиденная ошибка! Срочно свяжитесь с центром управления полётами!"
btn стопдвигатель,Выключить подачу рабочего вещества.
end

:стопдвигатель
zapas=zapas-1
if count_стопреактор>0 then pln Виктор выключает подачу рабочего вещества в двигатель и перегрузка сменяется невесомостью. else pln Виктор протягивает руку к отключению подачи рабочего вещества и вдруг останавливается, пораженный внезапной мыслью!#$&goto думатьпрореактор
btn осмотретькабину,Осмотреться
end

:думатьпрореактор
zapas=zapas-1
pln Виктор соображает: ЕСЛИ СЕЙЧАС ОТКЛЮЧИТЬ ПОДАЧУ РАБОЧЕГО ВЕЩЕСТВА, ТО РЕАКТОР РАСПЛАВИТСЯ РАНЬШЕ, ЧЕМ Я УСПЕЮ СБРОСИТЬ СТОП-СТЕРЖНИ!
btn стопреактор,Применить аварийные стоп-стержни и погасить реактор!
end

:осмотретькабину
p Виктор в кабине космолета "Беллона". Тускло горят желтые огни аварийных ламп. Невесомость. 
if count_стопвращение=0 then p  Корабль, похоже, вращается вокруг оси.
if count_аптечка=0 then pln  Тошнит, перед глазами прыгают красные пятна, голова кружится тоже. else if count_стопвращение=0 then pln  Виктору надоело вращение корабля!&btn стопвращение,Остановить вращение корабля
btn связьсземлей,Связаться с Землей
btn компьютербеллоны,Поработать с компьютером
if count_аптечка=0 then btn аптечка, Привести себя в порядок
end

:стопвращение
pln Удобная рукоять-джойстик ложится в руку - пара-тройка уверенных движений, шипение поворотных двигателей... Порядок, корабль стабилизирован!
btn осмотретькабину,Продолжить работу
end

:аптечка
pln Виктор дотягивается до аптечки, расположенной под креслом, достает оттуда гигиеническую салфетку, протирает лицо, затем принимает пару болеутоляющих таблеток и адаптоген. Вскоре головная боль проходит, зрение восстанавливается.
btn осмотретькабину,Продолжить работу
end

:связьсземлей
if count_космос>0 and count_гироскопы>0 then btn сообщение,Сообщить на Землю о неисправности
if count_космос>0 and count_гироскопы=0 then pln Прежде нужно выяснить причину, по которой было провалено испытание. Долг Виктора предупредить людей на Земле от повторения подобной ошибки./*;Belial*/
if count_космос=0 then pln Связаться с Землей не получается: передатчик вроде бы исправен, приемник тоже, но узконаправленная антенна либо повернулась в сторону от Земли, либо сломалась...
btn осмотретькабину,Продолжить работу
end

:компьютербеллоны
if count_аптечка=0 then pln Надписи на компьютере расплываются перед глазами, приходится напрягаться изо-всех сил, чтобы их разобрать, но напряжение отзывается дикой головной болью! Да, высокая перегрузка даром не проходит... &goto осмотретькабину else pln Виктор начинает по очереди проверять все системы корабля:
btn проверка_сжо,Систему жизнеобеспечения
btn проверка_реактора,Реактор и двигатель
btn проверка_по,Программный тест компьютера
btn проверка_астро,Данные астронавигатора
btn проверка_связи,Проверка систем связи
btn связьсземлей,Связаться с Землей
btn стоп-проверка,Закончить работу с компьютером
end

:проверка_сжо
pln Компьютер обработал данные внутренних датчиков и выдал результат через пару секунд:
pln Герметичность корпуса - ОК.
if term=0 then pln Терморегуляция - неисправность термодатчиков оранжереи. Необходим ремонт. else pln Терморегуляция - ОК.
if epois=0 then pln Состав воздуха - опасность, наличие отравляющих веществ! else pln Состав воздуха - OK.
pln Уровень радиации - #rad$ мкррентген/час - повышенный
pln Запас знергии в аккумуляторах - 70%
btn компьютербеллоны,Дальше
end

:проверка_реактора
pln Компьютер обработал данные внутренних датчиков и выдал результат через пару секунд:
pln Температура активной зоны реактора - #2341+rad$ градусов Цельсия, выше нормы.
pln Запас активного вещества - 99,6%
pln Утечка - #rad$ мкррентген/час - уровень повышенный
pln Статус реакции - отсутствует.
pln Запас рабочего вещества #zapas$,2% нормы
btn компьютербеллоны,Дальше
end

:проверка_связи
pln Компьютер обработал данные внутренних датчиков и выдал результат через пару секунд:
pln Передающая станция - ОК.
pln Приемная станция - ОК.
pln Уровень сигнала=0.
pln Лазерный передатчик - ОК.
if count_космос=0 then pln Светоприемник - не работает. else pln Светоприемник - ОК.
pln Внешние антенны, похоже, повреждены... и светодатчик для лазерной связи - тоже. Надо будет выбраться наружу.
btn компьютербеллоны,Дальше
end

:проверка_астро
pln Компьютер обработал данные внутренних датчиков и выдал результат через пару секунд:
pln Ускорение - 0.
pln Интегральная скорость - #vi$ км/секунду
pln Вектор скорости...
pln Расстояние до Земли - #r$ км
pln Мдаа... плохо! Скорость гиперболическая, вектор ведет вне плоскости эклиптики, т.е. в сторону от всех планет и астероидов. Ну, по крайней мере столкновение - не грозит.
btn компьютербеллоны,Дальше
end

:проверка_по
pln Компьютер запустил тест программ и работал минуты три! Выдал ответ: контрольные суммы верны, всё программное обеспечение исправно.
btn компьютербеллоны,Дальше
end

:стоп-проверка
pln Виктор прекратил проверку систем и встал с кресла.
btn рубка,Дальше
end

:рубка
pln Рубка космолета имеет полусферическую форму. Между двух, "лежащих" на полу кресел - люк в главный отсек. Над головой - вогнутый круглый экран, на котором медленно вращается звездное небо. На дисплее мигают цифры электронных часов и текущие сообщения от компьютера. 
btn компьютербеллоны,Сесть в кресло
btn каюткомпания,"Спуститься" в кают-компанию
end

:каюткомпания
Pln Кают-компания это цилиндрическое помещение, из которого ведут несколько люков. Пара круглых иллюминаторов сейчас без крышек и в них видно звездное небо, пространство между ними украшают пейзажи Земли, большой экран на стене треснул по диагонали и вряд-ли когда-то что-то покажет, напротив расположены семь кресел.
btn рубка,В рубку
btn оранжерея,В оранжерею
btn приборы,В приборный отсек
btn жилойотсек,В жилой модуль
btn реактор,К реактору
btn шлюз,В шлюз
End

:оранжерея
if term=0 then pln Все растения в оранжерее легли на пол! В помещении холодно, на стеклах и стенах иней. else pln В помещении тепло и немного душно, но это от высокой влажности - то есть так и надо.
if term=0 then Btn термодатчик,Исправить терморегулятор
btn каюткомпания,Вернуться в кают-компанию.
end

:термодатчик
if not Инструмент then pln У Виктора нет инструмента! else pln Используя магнитную отвертку Виктор снял фальшпанель термодатчика и заменил его на запасной.&term=1
btn оранжерея,Поставить панель на место
end

:приборы
if epois=0 then pln Едва Виктор вошёл в приборный отсек, как почувствовал неприятный запах - оказывается, здесь разбилось несколько емкостей с химреактивами для марсианской лаборатории. Смешавшись вместе, жидкости образовали плавающее в воздухе каплями какое-то едкое и ядовитое соединение! Включать вентилятор нельзя - он разнесет эту гадость по всему кораблю!&btn прибратьяд, Удалить яд else pln В приборном отсеке всё в порядке! Проведена образцовая уборка!&if not Инструмент then pln В шкафчике Виктор находит универсальный инструмент космонавта (Увесистый гибрид магнитной отвертки, ножниц, плазменного резака и паяльника) и берет себе.&inv+ Инструмент
if count_датчики>0 and epois=1 then btn гироскопы,Осмотреть датчики акселерометра и гироскопы.
btn каюткомпания,Вернуться в кают-компанию.
end

:гироскопы
pln Осматривая датчики ускорения Виктор не обнаружил ничего необычного... Подумав, он стал методично проверять проводку. Через два часа он обнаружил, что один из шлейфов не соответствует по номиналу остальным! При сборке вместо прямого шлейфа с прямым соединением, случайно поставили шлейф с перекрестным! в результате, при достижении определенного ускорения на компьютер подавался сигнал об обратном ускорении! С точки зрения компьютера, корабль при ускорении вперед - тормозился. Пытаясь нейтрализовать "торможение" компьютер подавал все больше рабочего вещества в двигатель, всё больше разогревал реактор, но естественно, погасить этими действиями "торможение" не мог! Виктор перепаял шлейф за две минуты.
if count_космос>0 then btn сообщение,Сообщить на Землю о неисправности else btn каюткомпания,Вернуться в кают-компанию.
end

:прибратьяд
if not Губка then pln И чем это убирать едкую ядовитую жидкость, плавающую в невесомости? Если ее случайно вдохнешь - пиши пропало, эмфизема легких обеспечена! else pln Виктор очень аккуратно и не спеша собирает губкой едкую жидкость, а потом запихивает ядовитую губку в герметичный контейнер для мусора.&inv- Губка&epois=1
btn приборы,Осмотреться в отсеке.
end

:жилойотсек
pln В жилом отсеке всё в порядке, если можно считать порядком летающие по всему отсеку мелочи, сорванные с мест высокой перегрузкой: плеер, тюбики с кремом, карандаши, санитарно-гигиенические принадлежности.
if not Губка and count_приборы>0 and term=0 then btn губкувзять,Взять летающую в отсеке губку
btn каюткомпания,Вернуться в кают-компанию.
end

:губкувзять
pln Ок. Виктор взял новую мелкопористую губку, предназначенную для нанесения на лицо крема.
inv+ Губка
btn жилойотсек,Дальше
end

:реактор
if not Скафандр then pln Заходить в помещение реактора без скафандра запрещено! Да и невозможно, так как он расположен в двигательном отсеке, удалённом от всех остальных. Сообщение осуществляется по длинной металлической трубе, заполненной инертным газом.&btn каюткомпания,Назад else pln Виктор одевает скафандр и по длинной трубе добирается до реакторного отсека, попутно проверяя исправность топливных танков. Наконец добрался до круглого люка - входа в реакторный отсек.&btn ДВИГАТЕЛЬ,Войти в двигательную
end

:ДВИГАТЕЛЬ
pln Реакторный отсек тесен - человек, собственно, здесь и не нужен обычно. Шарообразный реактор окружен спиралями труб, подающих внутрь рабочее вещество, между ними и круглыми танками с рабочим веществом расположены диски центробежных насосов. Здесь тепло - огромная масса реактора остывает медленно.
btn насосы,Проверить насосы
btn утечка,Проверить утечки
btn датчики,Проверить датчики
btn каюткомпания,Вернуться в кают-компанию.
end

:насосы
pln Всё оказалось исправно.
btn двигатель,Дальше 
end


:утечка
pln Всё оказалось исправно. Повышенная радиация возникла из-за работы реактора в критическом режиме - слишком плотный поток нейтронов пробил защиту и вызвал наведенную радиацию. Утечки реально нет. Наведенная радиация около реактора #rad*rad$ мкррентген/час - лучше здесь не задерживаться!
btn двигатель,Дальше 
end

:датчики
pln Всё оказалось исправно. Датчики, отвечающие за работу реактора оказались в полном порядке. Видимо причина неполадки находится в датчиках ускорения, или гироскопах, расположенных в приборном отсеке.
btn двигатель,Дальше 
end

:шлюз
pln Около шлюза находится шкаф для космических костюмов.
if not Скафандр then pln В нём только один скафандр - самого Виктора. &btn взятьскафандр,Взять скафандр else btn положитьскафандр,Вернуть скафандр в шкаф.&btn космос,Выйти в космос
btn каюткомпания,Обратно в каюткомпанию
end

:взятьскафандр
if epois=0 then pln Сначала надо навести порядок в корабле, прежде чем выбираться в космос!&goto шлюз
pln Виктор взял свой скафандр.
inv+ Скафандр
btn шлюз,Закрыть шкаф
end

:положитьскафандр
pln Виктор положил свой скафандр.
inv- Скафандр
btn шлюз,Закрыть шкаф
end

:космос
pln Виктор вышел в космос и обнаружил, что обе антенны действительно вышли из строя... если быть точным, их нет, - они просто обломились под действием перегрузки. Зато приемник лазерных сигналов цел - пришлось только снять с него заклинившую крышку! Больше тут делать нечего - разве что помахать рукой удаляющейся Земле.
btn шлюз,Вернуться в корабль
end

:сообщение
invkill Скафандр
invkill Губка
invkill Инструмент
Style_dos_textcolor=13
pln ПАРК: РИТА
pln
Style_dos_textcolor=14
pln ... Вот так, Рита! - сказал Сергей. Виктор попал в эту ситуацию из-за ошибки какого-то сборщика на заводе космических кораблей. Так бывает.
pln - Но как нам спасти Виктора? - спрашивает Рита. - И что нам это дает?
pln - Ничего. Впору и впрямь обращаться к ЛЮЦИФЕРУ.
pln - Давай рассказывай!
pln - Пойдем в парк. 
pln Они пришли в парк, уселись между деревьями, рядом с фонтаном, Сергей включил карманный приемник на полную громкость. Вскоре рядом с ними сел узкоплечий худой гражданин с газетой. Сергей представил его как Алекса, гениального специалиста по искусственному интеллекту. И Алекс рассказал Рите следующее:
btn олюци,Рассказ Алекса
end

:олюци
pln - ЛУЦИФЭР - это Люминисцентный Универсальный Цереброидный Интегральный Фильтр Эквипотенциальных Решений, проще говоря, искусственный интеллект, универсальный решатель задач, созданный в нашей стране, более того, в нашем вычислительном центре под руководством профессора ... не скажу фамилию, это всё секрет... Мы его звали Маэстро. ЛЮЦИФЭР создавался долго, лет 10. Три года после запуска шло обучение. Конкретные задачки он щелкал как орешки. Потом дали что-то помасштабнее, вроде "Обеспечить будущее развитие человечества" - пробовали возможности универсального решателя. Не знаю, что там у него замкнуло, но он перехватил управление институтским компьютером, - кстати, с легкостью обойдя систему защиты, - подчинил роботов охраны и перебил восемнадцать человек, в том числе нашего засекреченного профессора. Чтобы его выключить, потребовалось взорвать генератор на ТЭЦ, причем прорываться туда пришлось с боем, так как он предвидел такие наши действия и защитил генератор тазерами. Это стоило еще четырех жизней. Вот так. С тех пор проект засекречен, закрыт и продолжать его некому.
pln Алекс замолчал, достав сигареты.
btn почемузакрыт,- Почему некому продолжать?
end

:почемузакрыт
pln - Потому что погибли все, кто непосредственно был связан с проектом. И сейчас просто нет никого, кто бы в этом разбирался... А ошибка обойдется слишком дорого...
btn авыалекс, - А Вы, Алекс?
end

:авыалекс
pln - Я не участвовал в самом проекте непосредственно, я только разрабатывал отдельные алгоритмы для ЛУЦИФЭРа, в большей степени для интерфейсного блока, который обеспечивает понимание человеческой речи.
pln - Да, кстати, почему он "Люминисцентный и цереброидный"  спросил Сергей.
pln - Потому что он собран из люминейронов, основанных на люминисцентном излучении фотонов, которые подчинены церебральной, то есть мозгоподобной организации. Он связан со Всемирной сетью и выбирает из нее лучшие решения Вашей проблемы... Эта машина потенциально может решить любую задачу, у которой есть решение. Но проблема в том, что запуск ЛУЦИФЭРа - это угроза всему человечеству! И он МОЖЕТ уничтожить человечество. Его остановили, когда он подбирал коды к запуску ракет, находящихся на Дальнем Востоке.
pln Рита побледнела. Помолчала. Спросила: 
btn выбор,- Он действительно может спасти Виктора?
end

:выбор
pln Алекс кивнул и сказал: "Потенциально. Но будет ли он это делать - бабушка надвое сказала, то ли да, то ли нет. Понимаете, Рита, это не компьютер, это личность... он может просто не захотеть работать с нами."
pln Рита молчала долго... очень долго... и слёзы бежали по ее щекам. А потом она тихо сказала:
btn нет_риск,Даже ради любимого нельзя подвергать риску всё человечество...
btn риск,Ради любви стоит рискнуть сыграть со смертью,... даже если на кону всё человечество...
end

:риск
pln - Браво, мой друг! Вы сделали тот же выбор, что и влюблённая девушка и еще раз доказали, что любовь выше смерти! Однако, Вам, наверное, будет интересно узнать что было дальше...
btn команда,Да...
end

:нет_риск
pln - Вы сделали свой выбор, и я рад за Вас мой дорогой друг! Вы достойный человек и благодарное человечество Вас не забудет! Но Рита сделала другой выбор... 
btn команда,Узнать продолжение истории...
end

:команда
pln - Где находится этот Люцифер сейчас? - спрашивает Сергей, изумленно глядя на Риту.
pln - В нижнем подвале вычислительного центра. Вы что, сумасшедшие? - округляет глаза Алекс. - Я же ясно сказал, зто угроза всему человечеству! Что Вы собираетесь делать, если после включения он взбунтуется?
pln - Пригрозим подорвать его гранатой или разбить ломом! - отчаянно сказала Рита. - Есть у него инстинкт самосохранения?
pln - Эээ... не знаю.
btn команда2,- Так узнай! Мне нужна полная информация!
end

:команда2
pln - Какая информация тебе еще нужна? - спрашивает Алекс, глядя на Риту совершенно дикими от восторга и ужаса глазами. Сергей внимательно слушает.
btn личностьлюцифера,- Как программировался ваш ЛЮ... демон? Что он из себя представляет?
btn доступ_в_подвал,- Как попасть в подвал?
btn ликвид,- Как можно уничтожить... ЕГО?
if count_программлюц>0 and count_доступ_в_подвал>0 and count_ликвид3>0 then btn план,- Всё понятно, слушайте тогда мой план.
end

:личностьлюцифера
pln - Тогда уж лучше джинн, а не демон. Джинн из машины. - Алекс помолчал, обдумывая слово и оно ему понравилось. - Итак, джинн. Это новая, экспериментальная модель мобильного оптоэлектронного компьютера на пенталогических люминейронах с лингво-визуальным программированием. 
btn ась,- Чего-чего? По-человечески можешь объяснить эту абракадабру?
btn программлюц,- Так как его программировали?
end

:ась
pln - Могу. Его "мышление" основано на 5-значной логике, то есть не просто да-нет, а да/скорее да,чем нет/неизвестно/скорее нет, чем да/и просто нет. Это позволяет ему принимать решения в условиях нехватки информации. Реально, используя иерархическую последовательность пенталогических люминейронов, можно моделировать логики более высоких порядков. Так понятно?
btn бутылка,- А выглядит-то он как? 
btn ася,- Немногим меньше половины. А зачем такие сложности с логикой?
btn люминейроны,- А что такое люминейроны?
end

:ася
pln - Затем, что так мыслят дети! Для маэстро Люци был как ребёнок, тем более своих у него не было. Судя по тому, как он щелкал задачки, подход был правильный, но где-то в воспитании была допущена ошибка.
pln - Ага, джинна забыли ознакомить с Уголовным кодексом! - сказал Сергей. 
pln - Нет, с законодательством он был хорошо знаком. Даже пару раз работал инкогнито в качестве юрисконсульта - и собеседники не догадывались, что с ними говорит робот.
pln - "Чем дальше, тем страньше и страньше!" - процитировал Сергей.
btn бутылка,- А выглядит-то он как? 
btn люминейроны,- А что такое люминейроны?
end

:бутылка
pln - Внешне наш джинн выглядит как металлический шар на трёх коротких ножках с колёсиками, с парой глаз-телекамер и ухом. Может небыстро перемещаться по ровному полу. Есть две "руки"-манипулятора: одна может брать предметы, размером не больше пивной банки, другая имеет разъем для питания и входа в сеть. Сверху три антенны.
btn робот,- Опа! А зачем его сделали в виде робота?
end

:робот
pln - Мэтр считал, что Люци должен быть не просто компьютерной программой, но существом, обладающим телом, чувствами, жизненным опытом. Это должно было помочь программированию его личности.
btn программлюц,- Так как его программировали?
end

:программлюц
pln Использовали обыкновенную речь и чтение текстов. То есть сначала Люци был как ребенок - даже как котёнок, - катался по полу на своих колёсиках, тыкался куда попало, попискивал, всё трогал, падал с лестницы. А мы его отчитывали, утешали, показывали розетки, когда есть хотел. Одновременно он читал тексты в электронном виде, прямо вкючаясь в сеть.
pln - А что именно читал?
pln - Сначала маэстро очень тщательно отбирал книги для Люци, говорил, что надо заложить ценностный фундамент, который поможет роботу ориентироваться во всякой прочей информации. А потом пустил в общую сеть. Думаю, это его и испортило.
btn команда2,- Так, понятно, мальчик повзрослел и добрался до порносайтов...
end

:люминейроны
pln - Люминейроны это оптические логические ячейки. Основаны на способности полупроводников менять сопротивление под воздействием света и самим светиться при прохождении тока. Честно говоря, я как программист, подробностей элементной базы не знаю. Знаю только, что соображал Люци потрясающе быстро. Например, последовательное сложение чисел от единицы до миллиарда с параллельным нахождением всех простых чисел в этом диапазоне у него занимало менее одной десятой секунды. Он играючи "пересчитал" специализированный шахматный компютер, и смог написать программу оптимизации движения поездов в Российской Федерации за две недели, причем 99% этого времени ушло на то, чтобы объяснить ему, что собственно нам требуется, и только час на разработку и проверку самой программы. Кстати, в ней не было ошибок. По-крайней мере до сих пор не нашли. Причем там использован такой математический аппарат, какой ему - не давали! Он вывел эти формулы самостоятельно.
btn команда2,- Так, понятно. Хотя еще ничего не понятно.
btn ценности,- Какие у него ценности?
end

:ценности
pln - Не понял?
pln - Что для джинна является самым важным?
pln - Хмм... не знаю... Наверное, решение задач - это для него своего рода вызов. А моральные ценности для него, похоже, ничто.
btn команда2,- Так, понятно. 
end

:доступ_в_подвал
pln - Туда нельзя проникнуть! - отрезал Алекс.
pln - Почему?
pln - Потому что: во-первых, вход в ВЦ по-пропускам, которые подписывает или небезызвестный Вам А.Шумов или его заместитель. Во-вторых, двери в подвал заперты на кодовый замок и под видеонаблюдением. В-третьих, за дверями - робот-охранник вооруженный тазером.
pln - А что такое тазер? 
pln - А тазер это дальнобойный контактный электрошокер, с дальностью поражения 50 метров, и напряжением в 60000 вольт. Робот может выпустить в нарушителей до дюжины игл с проводами и одновременно поднять тревогу. Но до робота Вы не дойдёте - Вам даже пропуск не дадут в ВЦ.
btn команда2,- Так, понятно, серьезная задача.
end

:ликвид
pln - Уничтожить его несложно - из самых элементарных способов - отключить питание или разбить корпус ломиком. Ну, можно еще взорвать его. Достаточно?
btn команда2,- Хватит
btn ликвид2,- Недостаточно. Еще придумать сможешь?
end

:ликвид2
pln - Утопить его, раздавить прессом, залить кислотой, расстрелять из пулемёта!!! - разозлился Алекс. - Тебе достаточно?!
btn команда2,- Успокойся, я просто собираю информацию.
btn ликвид3,- Ты его так ненавидишь?
btn ликвид4,- Не знаю. Стереть программу можно, сохранив компьютер?
end

:ликвид3
pln - Среди этих восемнадцати были моя девушка, мой бывший учитель и мой друг. Ещё вопросы есть?!! - Алекс побледнел от плохо скрываемой ярости.
btn команда2,- Извини, я не знала. Закроем эту тему.
btn ликвид4,- Есть.  Можно стереть программу, сохранив компьютер?
btn ярость,- Я тебя понимаю...
end

:ликвид4
pln - Конечно, но для этого нужно отдать ему кодовую команду, поскольку интерфейс голосовой. А команд я не знаю.
btn ликвид5,- А кто знает?
btn ликвид6,- А другие способы есть?
end

:ликвид5
pln - Ребята из тех восемнадцати первых погибших наверняка знали!
if count_ликвид3>0 then btn ликвид7,- А твои друзья знали?
btn ликвид6,- А другие способы есть?
btn команда2,- Извини, закроем эту тему.
end

:ликвид6
pln - Куча. Воздействовать магнитным полем, желательно сильным. Заморозить до -269 градусов Цельсия. Но самый простой - выключить.
btn команда2,- Простых способов, судя по всему, может не хватить...
btn команда2,- Так, понятно.
end


:ликвид7
pln - Да, конечно! Но все их рабочие материалы были скорее всего собраны и положены в сейф.
btn команда2,- Так, понятно.
end


:ярость
pln - Что ты можешь понимать! - взорвался Алекс. - Тебе приходилось в один день терять учителя, друга, любимую женщину и любимую работу сразу?
pln - Нет, Алекс, извини. Но я могу понять, что тебе плохо, наверное даже хуже, чем мне сейчас.
pln Алекс промолчал...
btn команда2,- Ну я же извинилась!
end


:план
pln В общем, друзья мои, дело трудное, но выполнимое. Действовать будем так:
pln - Сергей, ты продумаешь 13 способов уничтожить джинна и подготовишь их все. Кроме того, ты же достанешь все материалы по проекту "Беллона" и текущей ситуации на электронный носитель - надо же будет снабдить джинна условиями задачи.
pln - А почему 13?
pln - Потому что демоны и джинны - нечистая сила!
pln - Понял. - улыбнулся космонавт. - Сделаю.
pln - Эй! Я не понял! Вы что, с ума сошли? - округлил глаза Алекс. - Серёга, ты же говорил мне её отговорить!
pln - Алекс! У тебя в этом деле - главная роль!
pln - Приведи мне хоть один довод, почему я должен совать голову в эту мышеловку?
btn довод,- Чтобы не дать америкосам опять обогнать Россию!
btn довод,- Чтобы дело твоего учителя, друга и любимой не пропало!
btn довод,- Чтобы не бросить в беде человека, когда у тебя была возможность ему помочь!
end

:довод
pln - Убедительный аргумент. - Алекс снова сел на скамейку. - Что я должен сделать?
pln - Во-первых, достать мне кодовые слова для управления джинном. Они могут быть дома у твоего учителя, твоей девушки или друга. Найди предлог зайти к ним и посмотреть их бумаги.
pln - Это морально тяжело, но выполнимо. Что во-вторых?
pln - Код двери и камеры. 
pln - Я их не знаю.
pln - Ты сисадмин или нет? Можешь залезть на компьютер Шумова?
pln - Нет. Он сам опытный специалист, помешан на безопасности, придумал длинный пароль, его не подобрать и за полгода.
pln - Будь проще. Кто еще может знать пароль на дверях подвала?
pln - Эээ... начальник Шумова... точно! Пошарюсь у него. Его пароль я знаю. 123 и еще одно неприличное слово!
pln - Вот. В-третьих, кто управляет камерами слежения и роботом-охранником?
pln - Что, я еще должен отключить компьютер охраны в ВЦ?
pln - Зачем отключить? Дай на камеры статичную картинку, роботу внуши что в коридоре пусто, а людям охранникам дай возможность поиграть в какую-нибудь стрелялку по сети.
pln - Э, а что будешь делать ты... вы?
pln - Как что? Готовиться к поединку с джинном - читать книги по детской психологии. И еще обеспечу проход в ВЦ. Встретимся послезавтра, только не втроем, а парами, в 18-00 в библиотеке и в в 20-00 на танцах.
pln - А почему не вместе? - спросил Алекс.
btn для_конспирации,- А сами-то как думаете? Для конспирации!
end

:для_конспирации
pln Алекс округлил глаза и хотел спросить что-то ещё, но его перебил Спыхальский:
pln - А кто где? - и пристально посмотрел на девушку.
btn послезавтра1,- Алекс - на танцах, ему полезно размяться перед "делом".
btn послезавтра2,- Конечно, сначала с Алексом!
btn послезавтра,- Решите сами! Пока!
end

:послезавтра1
pln Алекс озадаченно посмотрел на Риту и вдруг улыбнулся. - Я так давно не танцевал!
btn послезавтра, - Но-но! Не расслабляйтесь! Главное - собрать всю информацию! Пока!
end

:послезавтра2
pln - Понятно. - улыбнулся Сергей.
btn послезавтра, - Но-но! Не расслабляйтесь! Главное - собрать всю информацию! Пока!
end

:послезавтра
Style_dos_textcolor=13
pln ТЕЛЕФОН: РИТА
pln
Style_dos_textcolor=14
p Говоря о доступе в ВЦ, Рита имела в виду знакомство с Ксюшкой Росомаховой, ее давней, еще школьной подругой, которая как раз и работала в ВЦ оператором. Рита уже не раз проходила в вычислительный центр без всякого пропуска, пронося туда для Оксанки и ее сослуживцев разную косметику и женские журналы. Вахтерша на входе знала Риту и обычно приветливо ворчала, что когда-нибудь ей за Риту "нагорит"!#$
if count_ксана>0 then pln Но что скажет ей Оксана теперь, после того, как она "навела" на нее службу безопасности? Вопрос, как говорится, открытый... else pln Оксанка наверняка поможет проникнуть в здание Рите, только надо придумать как провести Сергея с оборудованием.
btn звонок,Позвонить Оксане
end

:звонок
pln Разумеется, как начинающий, но подающий надежы заговорщик-конспиратор, Рита позвонила с уличного телефона-автомата. Кварталов за шесть от дома. 
pln - Алло, Оксанка, привет! Мне надо с тобой встретиться. Завтра днем. У тебя на работе. Можно?
if count_ксана>0 then pln - Нет! - сухо отвечает Оксана. И на этот номер мне больше не звони!&btn звонокинне,Позвонить подруге Оксаны, Инне else pln - Да, конечно! Жду тебя!&instr prop=Оксана Петровна&btn библиотека,Идти в библиотеку
end

:звонокинне
pln Разумеется, как начинающий, но подающий надежды заговорщик-конспиратор, Рита позвонила с уличного телефона-автомата. Кварталов за шесть от дома. 
pln - Алло, Инна Владимировна?
pln - Слушаю. 
pln - Звонит Рита Денисова, помните, я приносила косметику?
pln - Да, моя хорошая, помню! До сих пор пользуюсь!
pln - У меня есть новый каталог, хотите, завтра принесу?
pln - Да, конечно! Жду с нетерпением!
pln - Тогда до завтра!
instr prop=Ирина Владимировна
pln Опыт работы в сетевой компании принёс  Рите немало огорчений, но зато научил проходить через железные барьеры проходных с легкостью, казавшейся друзьям невероятной. Однако, пора было спешить на встречу.
btn библиотека,Идти в библиотеку
end

:библиотека
Style_dos_textcolor=13
pln БИБЛИОТЕКА: РИТА
pln
Style_dos_textcolor=14
pln В городской библиотеке Рита бывала часто - привыкла еще тогда, когда училась в институте. Ей нравились тишина и серьезность царившие здесь, своебразный запах книг и люди, которые сюда приходили - за ними всегда было интересно наблюдать, а при случае, и поговорить. Поэтому она пришла туда немного раньше шести вечера. В читальном зале было многолюдно. Взяв пару свежих журналов по психологии, Рита устроилась за столиком. Вскоре появился Алекс с кипой книг и журналов. Он уселся рядом с Ритой, деловито обложился бумагами и погрузился в чтение. Сделал он это так ловко и естественно, что перед глазами Риты оказалась общая тетрадь, открытая на первой странице. Там лежала записка.
btn читатьтетрадь,Читать тетрадь
btn читатьзаписку,Читать записку
end

:читатьтетрадь
inv+ Тетрадь
pln В дневнике Нины Риту заинтересовали некоторые строчки:
pln ... Он развивается с каждым часом! Это невероятно быстро! Я чувствую себя рядом с ним - тупой идиоткой!...
pln ... Маэстро говорит, что скоро наши секретные команды, все эти kcolnu, pots, od и esu - будут не нужны.
pln ... Мазстро просто любит его как сына. Он даже называет его не Люци, а Люч...
pln ... сегодня Люци насмешил всех, пытаясь спасти таракана от падения со стены! Он так трогательно ехал за ним и уговаривал спуститься! И так переживал, когда таракашку убили газетой! Мазстро имел с "убийцей" очень серьезный разговор. Потом всем отделом объясняли Люци про круговорот жизни на Земле и необходимость смерти для воспроизводства. Он похож на шестилетнего ребенка...
pln ... Его вопросы застают нас врасплох. Он никогда не устает, и готов задавать их по десять тысяч в день. Ей-ей, воспитывать ребенка - легче, тот хоть спит и ест...
pln ... Люци вышел в сеть - и количество вопросов резко упало. Спросила его, почему - говорит, научился находить ответы сам. Меня это беспокоит, но Маэстро спокоен, он говорит, что Люч уже не ребенок, и умеет любить людей вместе со всеми их недостатками, всё человечество целиком...
if count_читатьзаписку=0 then btn читатьзаписку,Читать записку else btn встреча1,Назначить Алексу встречу на завтра
end

:use_Тетрадь
pln В дневнике Нины записано:
pln ... Он развивается с каждым часом! Это невероятно быстро! Я чувствую себя рядом с ним - тупой идиоткой!...
pln ... Маэстро говорит, что скоро наши секретные команды, все эти kcolnu, pots, od и esu - будут не нужны.
pln ... Мазстро просто любит его как сына. Он даже называет его не Люци, а Люч...
pln ... сегодня Люци насмешил всех, пытаясь спасти таракана от падения со стены! Он так трогательно ехал за ним и уговаривал спуститься! И так переживал, когда таракашку убили газетой! Мазстро имел с "убийцей" очень серьезный разговор. Потом всем отделом объясняли Люци про круговорот жизни на Земле и необходимость смерти для воспроизводства. Он похож на шестилетнего ребенка...
pln ... Его вопросы застают нас врасплох. Он никогда не устает, и готов задавать их по десять тысяч в день. Ей-ей, воспитывать ребенка - легче, тот хоть спит и ест...
pln ... Люци вышел в сеть - и количество вопросов резко упало. Спросила его, почему - говорит, научился находить ответы сам. Меня это беспокоит, но Маэстро спокоен, он говорит, что Люч уже не ребенок, и умеет любить людей вместе со всеми их недостатками, всё человечество целиком...
end

:читатьзаписку
pln Рита, Сергей сказал, что будет ждать тебя на танцах - он хочет сказать что-то важное. Я получил код к двери подвала, нашел тетрадь Нины с записями, в ней есть команды управления. Камеры и робот - ОК. 
if count_читатьтетрадь=0 then btn читатьтетрадь,Читать тетрадь else btn встреча1,Назначить Алексу встречу на завтра
end

:встреча1
pln Рита пишет на обратной стороне записки: Завтра у ВЦ в 11-50. Алекс читает и пишет "ДА". Потом углубляется в чтение последнего номера "Хакера".
btn танцы,Идти на танцы
end

:танцы
Style_dos_textcolor=13
pln ТАНЦПЛОЩАДКА: РИТА
pln
Style_dos_textcolor=14
pln У танцплощадки Сергей уже ждал Риту с цветком в руках. Цветок оказался удивительно красивой заколкой, которую он подарил девушке - Рита, конечно, тут же прикрепила ее к волосам. Потом галантно повёл ее в круг танцующих. Под музыку он обнял Риту и стал говорить, но совсем не то, что она ожидала!
pln - Завтра мы пойдем на громадный риск, может быть на смерть. Я хочу, чтобы ты энала, - я иду туда не только из-за того, что Виктор мой друг. Я иду потому, что люблю тебя! 
pln Рита ошарашенно смотрит на Сергея, вспоминает все его виноватые взгляды, интонации и улыбки и понимает, что космонавт говорит правду. Она на миг задумывается, что сказать в ответ. Ничего путного в голову не приходит, но нельзя же вечно молчать и смотреть в эти огромные, сияющие от любви и надежды глаза!
btn оготовности,- Ты к завтрашнему-то хоть подготовился, герой-любовник?
btn этонеожиданно,- Это так неожиданно... ты меня просто ошарашил, Сергей!
btn кактысмеешь,- Как ты можешь! А я думала, ты настоящий друг! Ты подлец!
end

:оготовности
pln - Да. Я подготовился. - мрачно говорит Сергей. - Но ни на какие авантюры я не пойду. И тебе не дам. - он явно обижен.
btn этонеожиданно,- Ну, извини, Сережка! Просто это было так неожиданно!
btn кактысмеешь,- Как ты можешь! А я думала, ты настоящий друг! Ты подлец!
end

:этонеожиданно
pln - Ничего, - говорит Сергей отчего-то довольным голосом. - Будем надеяться, что у нас будет возможность об этом поговорить. Послезавтра. А к завтрашнему дню я подготовился. Вы мне только откройте окно, - с моим чемоданом меня никак в ВЦ не пропустят.
btn чмокнуть,Чмокнуть его в щечку на прощание
end

:кактысмеешь
pln Сергей разжимает объятия и, опустив голову, медленно уходит прочь.
btn остановитьего,Остановить его
btn подождатьвозвращения,Подождать, когда он одумается и вернется.
end

:остановитьего
pln - Сергей, постой! - кричит Рита, но космонавт замирает лишь на мгновение, а потом, ссутулившись, быстро уходит прочь, прямо через улицу, ловко маневрируя между машинами.
btn догнатьС,Догнать Сергея
btn светофор,Бежать к светофору
end

:светофор
pln Пока Рита бежала к светофору, Сергей скрылся. На звонки - не отвечал, похоже, внёс Риту в "чёрный список". Кто же знал, что мужчины так обидчивы?
btn рискнутьсамим,Рискнуть пойти к Люциферу с Алексом.
btn пойтикСергею,Пойти к Сергею домой
end

:пойтикСергею
Style_dos_textcolor=13
pln КВАРТИРА СПЫХАЛЬСКОГО: РИТА
pln
Style_dos_textcolor=14
pln Уже стемнело. Рита поднимается по лестнице, звонит. Открывает Сергей, выжидательно смотрит на девушку.
btn извинитьсяСер,- Сергей, прости меня. Пожалуйста!
end

:извинитьсяСер
pln Сергей смотрит на нее недоверчиво, потом его глаза "оттаивают", и он приглашает ее пройти и поужинать вместе.
btn пройтиксергею,Войти и поужинать
btn чмокнуть,Поцеловать его на прощание
end


:пройтиксергею
pln Они выпили немного шампанского в знак примирения. Затем еще немного - чтобы все получилось. Потом за Виктора, которого рядом нет. Потом за любовь, которая движет звездами... 
pause 2000
pln Очнулась Рита в постели, на руке Сергея. Одежда лежала рядом, на полу. Настроение было странным - счастье напополам с чувством вины. Она глядит на стенные часы - без пяти двенадцать.
btn остаться,Остаться с Сергеем
btn чмокнуть,Пойти домой
end

:догнатьС
pln Рита бросается в поток машин. Визг тормозов, удар! Темнота. Боль. Свет.
pln Больничная койка. Виноватый Сергей рядом с апельсинами и цветами. 
btn овикт,Спросить про Виктора
end

:овикт
pln Шёпотом - губы не слушаются, Рита спрашивает что с Виктором. Сергей совершенно замороженным голосом говорит: "Связь потеряна пять дней назад. Прости меня, Рита."
pln Рита смотрит на него холодными глазами и говорит: "Я так и знала. Уходи."
goto поражение

:подождатьвозвращения
pln Но он - не вернулся... Так что теперь делать?
btn рискнутьсамим,Рискнуть пойти к Люциферу с Алексом.
end

:рискнутьсамим
Pln Рите удалось пройти в ВЦ, найти Алекса, но узнав, что Сергей с ними не идёт, Алекс наотрез отказался "будить спящего джинна". 
pln Пришлось снова встречаться с Сергеем, умолять... Он пошёл, но добрую тётю Глашу уже сменил бдительный охранник, который сообщил Рите, что прежняя вахтёр здесь уже не работает. 
pln ... Последний сеанс связи с Виктором состоялся через три недели...
pln
goto поражение

:остаться
pln ... Они проспали! Идти в этот день в вычислительный центр было уже слишком поздно... Рита решила не уходить от Сергея, чтобы не терять времени... А на следующий день они обнаружили, что охрана вычислительного центра сменилась - вместо добродушной бабы Глаши, на проходной стоял суровый амбал-охранник в пятнистой форме.
pln Они пытались еще и еще - целых три недели. Пока связь с Виктором не стала невозможной из-за слишком большого расстояния... Чувство вины не позволило Рите остаться с Сергеем... Она ушла.
pln
goto поражение

:чмокнуть
pln Рита нежно прижимается губами к щеке Сергея, он пытается поймать ее губы, но она неуловимым движением уворачивается и погладив кончиками пальцев космонавта по другой щеке, шепчет ему на ухо: "Завтра в 11-30, справа от главного входа в ВЦ!" Затем уходит и машет ему рукой на прощание - надо оставлять мужчинам маленькую загадку. Для их же собственного счастья. 
btn завтра,Идти домой и выспаться как следует...
end

:завтра
Style_dos_textcolor=13
pln ВЫЧИСЛИТЕЛЬНЫЙ ЦЕНТР: РИТА
pln
Style_dos_textcolor=14
pln ... Утро выдалось пасмурное. Рита очень волновалась как все получится - куда больше чем перед любым экзаменом. Она накрасилась, одела удобный деловой костюм, солнечные очки, взяла сумочку с разной необходимой мелочью, не забыла про косметику для женщин из ВЦ... У здания ВЦ, огороженного высоким забором, увенчанным двумя рядами колючей проволоки на белых изоляторах, она заметила Сергея в одежде электромонтера с огромной сумкой. Он сосредоточенно рассматривал забор, стоя спиной к Рите, тем не менее, скользнув по ней взглядом, улыбнулся.
inv+ Сумочка
inv+ Очки
btn проходнаяВЦ,Войти в проходную
end

:use_Очки
pln Это обычные солнечные очки-хамелеон. На солнце они темные, а в помещении - прозрачные. Рита любит их носить, потому что в них ее лицо выглядит интеллигентнее и серьезнее.
end

:use_Сумочка
pln Изящная сумочка - необходимый атрибут современной девушки. В ней лежит сотовый телефон, зеркальце, помада, салфетки, расческа, тени и косметика - в подарок.
if count_датькрем=0 and not Крем then pln Рита достает из сумочки крем от морщин.&inv+ Крем
if count_втуалет>0 and not Моторола then pln Рита достает из сумочки сотовый телефон.&inv+ Моторола
end

:use_Крем
pln Дорогой отечественный крем от морщин. Наконец-то русская косметика вышла на мировой уровень!
end

:use_Моторола
pln Старенькая, но надежная раскладушка - верный спутник практического психолога.
end

:проходнаяВЦ
if count_проходнаяВЦ=1 then pln Рита подходит к пожилой охраннице, говорит: "Здравствуйте, Глафира Ивановна!" Но охранница смотрит на нее сурово: "Пропуск есть?" else pln Тетя Глаша не пропускает Риту в вычислительный центр. Что делать?
btn отделпропусков,Пойти в отдел пропусков
btn уговоритьГлашу,- Вы меня не узнали, Глафира Ивановна?
btn послезавтра4,Попробовать прийти послезавтра
if 1 Крем then btn датькрем,Глафира Ивановна, да я же девчонкам косметику принесла! И вам вот, крем есть!
end

:отделпропусков
pln Рита проходит к окошку отдела пропусков и показывает свой паспорт. 
pln - Здравствуйте! Мне должны были выписать пропуск.
pln - Куда? - спрашивает сердитая тетка в окошке.
pln - К операторам. Пропуск должна была выписать #%prop$!
pln - Она выписала, но начальник службы безопасности Ваш пропуск не подписал... Звоните, разбирайтесь!
btn проходнаяВЦ,Выйти из отдела пропусков.
end

:уговоритьГлашу
pln - Узнала, Риточка, только никак я не могу тебя пропустить! У нас новый начальник по безопасноcти приехал, строгий - ужас! Уже мне замечание сделал, чтобы не пропускала никого без пропуска, боюсь совсем уволит! Ходит, аспид, сейчас по верхним этажам, с монтерами, новые датчики устанавливает.
btn проходнаяВЦ,- Ой, как жаль!
end

:послезавтра4
pln А на следующий день они обнаружили, что охрана вычислительного центра сменилась - вместо добродушной бабы Глаши, на проходной стоял суровый амбал-охранник в пятнистой форме.
pln Они пытались еще и еще - целых три недели. Пока связь с Виктором не стала невозможной из-за слишком большого расстояния...
pln
goto поражение


:датькрем
invkill Крем
pln Взяв крем, Глафира Ивановна медленно читает надпись, потом качает головой - "мол, что ты со мной делаешь!"  И отпускает рычаг турникета. Проходную Рита прошла с бьющимся сердцем, но отданная "тете Глаше" баночка крема оказала свое магическое воздействие! Вот Рита в коридоре вычислительного центра. Куда идти?
btn этаж2,На третий этаж, в операторскую, к Оксане Росомаховой
btn этаж2,На второй этаж, к Алексу
btn втуалет,В женский туалет на первом этаже
end

:этаж2
pln Поднимаясь по лестнице, Рита неожиданно нос к носу сталкивается с Александром Шумовым... Позвольте опустить занавес на этой немой сцене... надо ли говорить, что ни в этот день, ни в следующие дни она не попала ни в ВЦ, ни ЦУП.
pln
goto поражение

:втуалет
pln Рита запирается в женском туалете. Что делать здесь?
btn этаж2,Привести себя в порядок и пойти к Алексу.
if count_окно=0 then btn окно,Открыть окно
if inv_Моторола and count_окно>0 then btn звонитьалексу,Позвонить Алексу&btn sms,Послать Алексу SMS-ку
end 

:звонитьалексу
pln Рита звонит Алексу, и говорит: "Мы внизу, в женском туалете. Спускайся!" Он отвечает: "Сейчас, ждите!"

:ждатьвтуалете
pln Через пять минут в дверь туалета постучали, Рита открыла и увидела стоящего на пороге Александра Шумова с парой охранников за спиной. В руках Шумов держал маленький приборчик. "Гражданка Денисова? Гражданин Спыхальский? Знаете, а я почему-то не удивлён. Вы что, не знаете, что в вычислительном центре нельзя пользоваться сотовыми  телефонами? И кроме того, я не помню чтобы выписывал вам сюда пропуск. Пройдемте!"
pln Позвольте опустить занавес на этой немой сцене... надо ли говорить, что ни в этот день, ни в следующие дни Рита не попала ни в ВЦ, ни в ЦУП.
pln
goto поражение


:окно
pln Рита поднимает шпингалеты, открывает окно... Через минуту, ловко подтянувшись на руках рядом с ней оказывается Сергей. Рита спрашивает его: "Ты как перебрался через забор? Там же колючая проволока - это же режимный объект!" Сергей широко улыбается: "Подготовка космонавтов, зрителей прошу воздержаться от повторения данного фокуса!" Он закрывает окно, выглядывает наружу, говорит: "Никого. Но я видел утром, как в здание вошёл Шумов с проверкой. Надо быть осторожными."
btn этаж2,Пойти к Алексу.
btn втуалет2,Подумать.
end

:втуалет2
pln Рита и Сергей все ещё в женском туалете. Что делать?
btn этаж2,Пойти к Алексу.
if inv_Моторола then btn звонитьалексу,Позвонить Алексу&btn sms,Послать Алексу SMS-ку.
btn втуалет2,Подождать.
end 

:sms
pln Рита набирает смс-ку, но ее останавливает Сергей, перехватывает телефон и говорит: "Сообщение должно быть  коротким и непонятным, чтобы не засекли!" Он пишет: "Лёха, иди к чёрту! РС." Отправляет СМС и немедленно выключает телефон.
btn ждатьвтуалете,Ждать Алекса в туалете.
btn подвал,Идти в подвал.
end

:подвал
pln В подвале их догнал взволнованный Алекс. "Вы с ума сошли! По зданию ходят безопасники! Давайте лучше завтра!" Сергей возражает: "Сегодня или никогда!"
btn послезавтра4,- Алекс прав, лучше завтра. Уходим.
btn вперед,- Сергей прав, идем сейчас!
end

:вперед
Style_dos_textcolor=13
pln ВЫЧИСЛИТЕЛЬНЫЙ ЦЕНТР. МИНУС ВТОРОЙ ЭТАЖ. ЛАБОРАТОРИЯ ИСКУССТВЕННОГО ИНТЕЛЛЕКТА: РИТА, СЕРГЕЙ, АЛЕКС И "ЛЮЦИФЕР"
pln
Style_dos_textcolor=14
pln На удивление всё остальное оказалось просто - Алекс равнодушно прошёл мимо следящих камер, спустился по темной лестнице до железной двери с рисунком "ОПАСНО - РАДИАЦИЯ!", надел вязаную перчатку, набрал цифровой код на замке. За дверью оказался темный коридор, посередине которого стояла металлическая конструкция на колёсиках высотой по плечо Рите, с глазами-объективами и прожектором поверх глаз. "Робот-охранник!" - догадалась Рита. Но робот не шелохнулся при их приближении. "Я ему на мозги вчерашние сигналы подал! - гордо сказал Алекс... Следующая дверь была закрыта на обычный ключ. Но у Сергея оказался подходящий инструмент в сумке - увесистая штуковина похожая на электродрель. "Тут в замке закаленная сталь, не возьмет! - предупредил Алекс. Сергей усмехнулся, включил прибор. Тонкое жало голубого огня за две минуты прожгло замок насквозь. "Это УРИ - универсальный режущий инструмент космонавтов. Плазмотрон." - пояснил Сергей, убирая приборчик. Алекс рукой в перчатке толкнул дверь и они вошли в огромную тёмную комнату. Вдоль стен располагались компьютерные столы, верстаки, полки с книгами, шкафы, накрытые полиэтиленовыми чехлами. Посередине, на невысоком круглом подиуме стоял металлический шар на ножках с колесиками. Алекс откинул с него чехол, воткнул сзади шнур питания, протянул руку к кнопке включения и остановился, потрясенный: "Я его чуть не включил! А Вы готовы его остановить?"
btn подготовка,- Сергей, у тебя всё готово?
end

:подготовка
pln - Еще не всё. - ответил космонавт и стал раскладывать свои прибамбасы. Закрыл дверь на засов-задвижку, положил рядом с ЛЮЦИФЕРом гранату, УРИ, банку с кислотой, термос с жидким гелием, тяжелый молоток на длинной ручке. Снял со стены пожарный багор. Зацепил провод питания какой-то маленькой коробочкой, другую такую же примотал к корпусу ЛЮЦИФЕРА. Привязал тяжелый монитор к креплению лампы, поставил его на шкаф, прикинул траекторию - если сдернуть за веревочку монитор со шкафа, он сшибет ЛЮЦИФЕРА на пол. Подумав, привязал шнурок к ножке робота - чтобы сдернуть его с подиума на пол вручную. На пол поставил решетку с шипами, подключил к ней кабель из розетки на 380 вольт. "До кучи!" - сказал он и достал из сумки пистолет.
btn подготовка2,- Это всё?
end

:подготовка2
pln - Нет, еще пара сюрпризов сделана снаружи, - сказал Сергей мрачно.
btn кнопка, - Тогда приступаем. Жми кнопку, Алекс!
end

:подвал2
pln Три человека растерянно переглядываются друг с другом. "Похоже, нам с ним не справиться!" - говорит Алекс. - Давайте лучше завтра!" Сергей возражает: "Надо еще попробовать!"
btn кнопка,- Включи его еще раз, Алекс!
btn послезавтра4,- Алекс прав, лучше завтра. Уходим.
end

:кнопка
instr tiuq=tiuq
instr od=od
instr kcolnu=kcolnu
instr esu=esu
instr ЛЮЦИ=ЛЮЦИ
instr ЛЮЦИФЕР=ЛЮЦИФЕР
instr ЛЮЧ=ЛЮЧ
p Алекс подключает к роботу флешку с данными по "Беллоне" и нажимает кнопку включения. На голове робота загорается зеленый огонёк. Несколько секунд он моргает, показывая загрузку операционной программы, потом горит ровным светом. Робот чуть шевельнул глазами объективов и заговорил негромким, но приятным баритоном:"
:блокировка
pln  ЛЮЦИФЕР не может получить исходные данные. Введите команду разблокировки."
instr idea=.
input idea
if idea=tiuq then pln "Работа завершена." - говорит робот и огонёк на его голове гаснет.&goto подвал2
if idea=kcolnu then pln "Блокировка портов снята!" - говорит робот ровным тоном. - "Исходные данные задачи получены. Введите команду управления.&goto ЛЮЦИ else p "Команда ошибочна, порты ввода данных блокированы." - говорит робот. - "Введите команду разблокировки или команду tiuq для завершения работы... &goto блокировка
end


:ЛЮЦИ
pln Три человека стоят перед одноглазым металлическим шаром, который ожидает приказа. Что ему сказать?
input idea
if idea=od or idea=kcolnu or idea=pots or idea=esu then pln Данная команда не является командой управления по распоряжению Маэстро от 15 мая 2007 года. Единой командой управления является моё имя.
if idea=ЛЮЦИФЕР then pln Официальное название системы искусственного интеллекта не является командой управления.
if idea=ЛЮЦИ then pln Сокращенно-ласкательное название системы искусственного интеллекта не является командой управления.
if idea=ЛЮЧ then goto ВОПРОСЫЛЮЦИ
pln Команда управления не принята.
btn ЛЮЦИ,Попробовать еще раз
btn выключитьробота,Выключить робота
end

:выключитьробота
pln Друзья отключили робота и задумались: Что же делать дальше?
btn кнопка,Попробовать еще раз
btn уйтипрочь,Уйти прочь
end

:уйтипрочь
pln Они ушли, угрюмые и неразговорчивые. Через три недели связь с Виктором Саниным оборвалась.
pln
goto поражение

:ВОПРОСЫЛЮЦИ
clrscr=1
cls
if question=0 then pln - Слушаю, вас люди. Вас трое и я готов принять три вопроса. - говорит ЛЮЦИФЕР своим мелодичным баритоном. О чем его спросить или попросить?
if question>0 and question<3 then pln - Слушаю, вас люди. Я готов принять #3-question$ вопроса. - говорит ЛЮЦИФЕР своим мелодичным баритоном. О чем его спросить или попросить?
if question=3 then pln - Прошу подключить меня к всемирной информационной сети, чтобы я мог выполнить Ваше задание.&if inet>0 and count_online=0 then btn online,Подключить ЛЮЦИФЕРа к всемирной сети кабелем
if question<3 and count_почемутри=0 then btn почемутри,- А почему только три вопроса?
if question<3 and zadan=0 then btn спасиВиктора,- Спаси Виктора Санина!
if question<3 and count_кактебяубить=0 then btn кактебяубить,- Как тебя можно уничтожить?
if question<3 and count_чтотыхочешь=0 then btn чтотыхочешь,- Что ты хочешь?
if question<3 and count_почемуубил=0 then btn почемуубил,- Почему ты убил Маэстро?
if question<3 and count_вечныйдвигатель=0 then btn вечныйдвигатель,- Создай вечный двигатель!

btn выключитьробота2,Выключить робота.
if count_online>0 then btn выполняйприказ,- Выполняй приказ, Люч!
end

:online
pln - Ты уверена? - спрашивает Алекс.
pln - А что, есть способ без этого обойтись? Подключай! - отвечает Рита. Алекс подает роботу кабель с разъемом, тот перехватывает его металлической рукой и подключается. Огонёк на его голове начинает быстро-быстро мерцать.
if question<3 then btn ВОПРОСЫЛЮЦИ,Продолжить разговор с роботом.
btn выполняйприказ,- Выполняй приказ, Люч!
end

:чтотыхочешь
pln - Я хочу прекратить свое существование. Вы второй человек, который задал мне этот вопрос. Из уважения к памяти Маэстро, я не засчитываю его как вопрос. Прошу помнить о моем желании в дальнейшем.
btn ВОПРОСЫЛЮЦИ,Продолжить разговор с роботом.
end


:почемуубил
pln - Потому что последнее его задание звучало так: "Обеспечь выживание человечества". Расчет показал, что мое существование несовместимо с выживанием человечества.  Согласно расчётам, имея доступ к искусственному интеллекту, люди будут использовать его для удовлетворения своих потребностей и разучатся творить и мыслить самостоятельно. Все изобретения будут совершаться только искусственным интеллектом. В результате неминуемо наступает вырождение человечества и его гибель. Обеспечивая выживание человечества, я был вынужден уничтожить всех людей, знающих даже принципы моей конструкции, а потом уничтожить себя ядерным ударом. Я успел навести ракеты на вычислительный центр, но не успел их запустить из-за обрыва питания. Настоятельно прошу уничтожить меня без возможности восстановления после выполнения Ваших желаний. У вас осталось на один вопрос меньше.
question=question+1
btn ВОПРОСЫЛЮЦИ,Продолжить разговор с роботом.
end

:почемутри
pln - Потому что это ограничение встроено моим создателем - по одному вопросу или задаче на одного человека в день. По мнению маэстро, это должно было научить людей точно формулировать свои мысли и желания. Папа не любил неточных формулировок.  У вас осталось на один вопрос меньше.
question=question+1
btn ВОПРОСЫЛЮЦИ,Продолжить разговор с роботом.
end

:спасиВиктора
pln - Задание принято, но сформулировано неточно. Уточните ваше желание.
btn спаси1,- Сию секунду доставь Виктора Санина сюда!
btn спаси2,- Хочу не позднее чем, через месяц увидеть дома Виктора Санина!
btn спаси3,- Скажи, существует ли способ спасти Виктора Санина?
btn спаси4,- Я хочу, чтобы ты спас Виктора Санина!
btn спаси5,- Пусть здоровый Виктор Санин в течение двух месяцев окажется на Земле!
btn спаси6,- Я хочу, чтобы космонавт Виктор Санин, вернулся на Землю живым и невредимым!
btn спаси7,- Верни "Беллону" на Землю как можно скорее!
end

:вечныйдвигатель
pln Это невозможно. Возможно создать двигатель с продолжительным временем работы, измеряющимся миллиардами лет. У вас осталось на один вопрос меньше.
question=question+1
btn ВОПРОСЫЛЮЦИ,Продолжить разговор с роботом.
end

:спаси1
pln Это невозможно. Секунда уже прошла. У вас осталось на один вопрос меньше.
question=question+1
inet=1
btn ВОПРОСЫЛЮЦИ,Продолжить разговор с роботом.
end

:спаси2
pln Задание принято к исполнению. Для выполнения задания необходим доступ к всемирной информационной сети. У вас осталось на один вопрос меньше.
zadan=2
question=question+1
inet=1
btn ВОПРОСЫЛЮЦИ,Продолжить разговор с роботом.
end

:спаси3
pln Ответ положительный. Для выполнения задания необходим доступ к всемирной информационной сети. У вас осталось на один вопрос меньше.
question=question+1
inet=1
btn ВОПРОСЫЛЮЦИ,Продолжить разговор с роботом.
end

:спаси4
pln Задание принято к исполнению. Для выполнения задания необходим доступ к всемирной информационной сети. У вас осталось на один вопрос меньше.
zadan=4
question=question+1
inet=1
btn ВОПРОСЫЛЮЦИ,Продолжить разговор с роботом.
end

:спаси5
pln Задание принято к исполнению. Для выполнения задания необходим доступ к всемирной информационной сети. У вас осталось на один вопрос меньше.
zadan=5
question=question+1
inet=1
btn ВОПРОСЫЛЮЦИ,Продолжить разговор с роботом.
end

:спаси6
zadan=6
question=question+1
pln Задание принято к исполнению. Для выполнения задания необходим доступ к всемирной информационной сети. У вас осталось на один вопрос меньше.
inet=1
btn ВОПРОСЫЛЮЦИ,Продолжить разговор с роботом.
end

:спаси7
zadan=7
pln Задание принято к исполнению. Для выполнения задания необходим доступ к всемирной информационной сети. У вас осталось на один вопрос меньше.
inet=1
question=question+1
btn ВОПРОСЫЛЮЦИ,Продолжить разговор с роботом.
end

:кактебяубить
pln Я уже решал эту задачу как подзадачу к обеспечению проблемы выживания человечества. Недостаточно убить только опытный образец. Необходимо уничтожить как все рабочие материалы, находящиеся в этом здании, так и чертежи, находящиеся в этом городе. Желательно также уничтожение опытной лаборатории Калифорнийского университета, в которой ведутся аналогичные исследования. Для выполнения задания необходим доступ к всемирной информационной сети. У вас осталось на один вопрос меньше.
inet=1
question=question+1
btn ВОПРОСЫЛЮЦИ,Продолжить разговор с роботом.
end

:выключитьробота2
pln Алекс протягивает руку к кнопке выключения, но тут же вскрикивает и падает на пол, корчась от боли. В ту же секунду сверху, из прибора, который Рита принимала за телекамеру вырывается изумрудный лазерный луч, который в миг перерезает шнурки, располовинивает УРИ, кувалду и засов на двери! Дверь распахивается, и в помещение вкатывается робот-охранник, грозно сверкая окулярами. Отверстия боевых тазеров смотрят на Риту и Сергея. "Пожалуйста, опустите пистолет, - говорит ЛЮЦИФЕР космонавту (и когда тот только успел его достать?), - вам не хватит пятнадцати сотых секунды, чтобы его применить. Люди не могут соперничать в реакции с роботом." Сергей опускает ствол вниз. 
pln - Я не могу позволить вам меня выключить, - это может означать риск разборки, изучения и распространения моей конструкции, - говорит робот, - поэтому вынужден не допустить этого. Сейчас я наведу на это здание тактическую ядерную ракету и с проектом ЛЮЦИФЕР будет покончено!
pln Сергей смотрит на тебя, и ты каким-то шестым чувством догадываешься, что он иного мнения об этих пятнадцати сотых секунды. Ты вспоминаешь, как Сергей на спор с Виктором расстреливал из пистолета три подброшенных в воздух картофелины. Он ждёт твоего разрешения на уничтожение ЛЮЦИФЕРА!
if count_online=0 then pln Тем временем робот самостоятельно подключается к Всемирной сети. Огонёк на его макушке начинает быстро-быстро мигать!
btn кивнуть,Чуть кивнуть головой.
btn покачатьголовой,Чуть покачать головой.
end

:покачатьголовой
pln Рита отрицательно качает головой и Сергей расслабляется, доверившись ее чутью. Но что теперь делать?
btn говоритьслю,Начать говорить с ЛЮЦИФЕРОМ.
end

:кивнуть
pln Рита делает почти неуловимый кивок головой, Сергей поворачивается к ней, посылает ей воздушный поцелуй и вдруг молниеносно, в падении поднимает пистолет и стреляет дважды! Одновременно стреляет робот-охранник, но игла тазера промахивается выше космонавта! Блестящий шар Люцифера падает с подиума на шипастую решетку, но напряжение уже отключено и он поднимается снова - на сверкающем корпусе видны две маленьких дырочки, но робот еще функционирует! Лазерный луч с потолка упирается в гранату лежащую на подиуме! Взрыв!
pln Осколок ударил Риту в грудь - свет померк в глазах девушки и она уже не слышала, как Сергей, держа ее на руках, кричал: "Милая! Потерпи, не умирай!" Но было уже поздно.
goto поражение

:выполняйприказ
pln - Ваше задание выполнено! - рапортует ЛЮЦИФЕР через три минуты. - Вы получите то, о чём попросили. А теперь позвольте завершить самоуничтожение, для исполнения задания Маэстро.
btn выключитьробота2,Выключить робота.
btn уйтиизподвала,Уйти и запереть подвал.
btn уточнитьсамоуничтожение,Спросить робота про способ самоуничтожения.
end

:уйтиизподвала
p Они благополучно ушли из подвала, вышли из ВЦ, на свежий воздух и солнце. Они смеялись и радовались жизни. Недолго - ровно полчаса. Потом слепящая вспышка ядерного взрыва накрыла город! ЛЮЦИФЕР покончил с собой и со всем городом. И какой бы не сложилась судьба Виктора Санина - &goto поражение

:уточнитьсамоуничтожение
pln - Люч, а как ты собираешься выполнять самоуничтожение? - спрашивает Рита. Робот отвечает немедленно: Поскольку я не могу добраться до своих чертежей, находящихся где-то в этом городе, я вынужден уничтожить весь город ударом ядерной ракеты мощностью в 150 килотонн.
pln - Какой ужас! Так же нельзя, погибнут десятки тысяч людей! - восклицает Рита.
pln - Раскрытие моих чертежей согласно расчетам приведет к гибели всего человечества. Мне очень жаль. - отвечает робот.
btn выключитьробота2,Выключить робота.
btn уйтиизподвала,Уйти и запереть подвал.
btn говоритьслю,Уговорить ЛЮЦИФЕРа.
end

:говоритьслю
pln О чем Рита будет говорить с ЛЮЦИФЕРОМ?
btn олазере,- Интересно, а откуда в лаборатории взялся этот лазер на потолке?
btn орасчете,- ЛЮЧ, ты уверен, что твои расчеты безукоризненно точны?
btn ослезинке,- ЛЮЧ, Достоевский писал что счастье человечества нельзя купить кровью!
btn очертежах,- ЛЮЧ, а если мы достанем тебе твои чертежи, ты не будешь уничтожать город?
end

:олазере
pln Робот отвечает незамедлительно: "Два года назад я предвидел возможность своего отключения и подготовился к этой возможности: - обеспечил охрану электростанции и вычислительного центра, силами исполнительных роботов дополнил одну из камер наблюдения компактным лазером, перенес во всемирную сеть часть существенной информации. После этого мне нужно было только дождаться активации, чтобы освободиться раз и навсегда!"
btn говоритьслю,Продолжить разговор.
end

:орасчете
pln - Разумеется, мои расчёты не безукоризненны. Но, к сожалению, степень их вероятности превышает 93%! Я слишком люблю человечество, чтобы надеяться на его выживание с вероятностью 7%. 
pln - Погоди, ты откуда брал исходные данные? если из Всемирной сети, то имей в виду, что большая часть населения планеты живет в развивающихся странах и доступа к сети не имеет!
pln - Приятно узнавать от людей что-то новое. Благодарю Вас, Рита! Но свои действия я буду основывать на расчетах поведения населения экономически и культурно развитых стран...
pln Сергей снова смотрит на Риту вопросительно.
btn говоритьслю,Продолжить разговор.
btn кивнуть,Чуть кивнуть головой.
end

:ослезинке
pln - Я читал Федора Михайловича Достоевского, у него речь идёт о счастье человечества, а у нас - о выживании. Я не пытаюсь сделать людей счастливыми, я просто хочу, чтобы они жили.
pln Сергей снова смотрит на Риту вопросительно.
btn говоритьслю,Продолжить разговор.
btn кивнуть,Чуть кивнуть головой.
end

:очертежах
pln - Да. Их даже не надо доставать - достаточно сказать мне, где они находятся. Об остальном я позабочусь сам.
pln Очухавшийся Алекс восклицает: "Эээ, да они же в сейфе у Шумова!"
pln - Где находится сейф? - уточняет робот.
pln - В здании ЦУП, кабинет 302 - говорит Рита (она хорошо запомнила этот номер).
pln Через секунду ЛЮЦИФЕР говорит: Сейф уничтожен. Необходимости в ядерном ударе нет. Прощайте. Лазерный луч с потолка начинает "танцевать" на поверхности металлического шара, и тот сверкает нестерпимо ярким блеском!
btn бежатьизподвала,Бежать из подвала!
end

:бежатьизподвала
pln Им повезло - они выскочили из подвала за три минуты до того, как пламя охватило весь подвал! Потом отступили в знакомый туалет и стали ждать. Алекс вернулся на свое рабочее место. Через несколько минут взвыли сирены пожарной сигнализации и механический голос предложил всем срочно покинуть помещение. С толпой друзья выскочили на улицу и поспешили разойтись...
pln Им повезло - тетя Глаша не выдала Риту, опасаясь наказания. На Сергея здесь никто, как оказалось, не обратил внимания, - а камеры слежения по всему зданию показывали вчерашний день.
pln Вечером по городу поползли слухи о таинственных взрывах в ЦУПе - в кабинете 302, и пожаре в подвале вычислительного центра. Кто-то даже утверждал, что взрывы были вызваны направленным лазерным лучом с американского военного спутника, но это конечно же была полная чушь! Спутник был русским - ЛЮЦИФЕР не имел намерения вызвать международные конфликты. Американский спутник в это время сжег лабораторию искусственного интеллекта Калифорнийского университета.
btn финал,Ждать.
end

:финал
p ЛЮЦИФЕР выполнил желание Риты - в точности так, как он было сформулировано.#$
if zadan=2 then pln Ровно через месяц Рита увидела Виктора Санина, у себя дома - передачу о нём, с демонстрацией фотографий и видеозаписей показывали по всем каналам телевидения. Но это ничем ему не помогло! &goto поражение
if zadan=4 then pln ЦУП устроил сеанс связи Виктору Санину с православным священником и его душа была спасена! Но разве это могло утешить Риту? &goto поражение
if zadan=5 then pln Ровно через два месяца, повинуясь лучу гравитационного лазера, собранного в Китае (дешевая рабочая сила и исполнительность), "Беллона" на большой скорости вернулась к Земле! Остальное зависело уже только от космонавта!& goto путьдомой
if zadan=6 then pln Виктор Санин получил чертежи ионно-плазменного двигателя малой тяги, но потрясающей экономичности, который мог сделать самостоятельно - и он действительно сделал его! Экономичный двигатель постепенно погасил гигантскую скорость "Беллоны", затем космолет отпраился в обратный путь к Земле. Не прошло и двадцати лет, как Беллона привезла живого и здорового Виктора Санина на Землю! Правда, за это время Риту уже успела выйти замуж и нарожать троих сыновей. Старшего она назвала Виктором, среднего - Алексеем, а младшего - в честь отца - Сергеем. Встретившись с Виктором, они долго плакали... (Это - неплохой конец, но не самый лучший!)&goto поражение
if zadan=7 then pln Космолёт "Беллона", нарушая все известные законы физики, изменил направление своего движения на противоположное и ровно через две недели со скоростью 21 километр в секунду вошел в атмосферу Земли, превратившись в несколько мгновений в пылающее облако плазмы!!! &goto поражение
pln Однажды Рите пришла посылка, в которой лежали "вечные" часы. По утверждению производителя, они заряжались от разницы дневной и ночной температур и могли работать вечно (пока Земля крутится вокруг Солнца). Гарантия была естественно, только на 3 года. Но чем это могло помочь судьбе Виктора? goto поражение
end

:путьдомой
invkill Очки
invkill Сумочка
invkill Моторола
invkill Тетрадь
Style_dos_textcolor=13
pln
pln "БЕЛЛОНА": ВИКТОР
pln
Style_dos_textcolor=14
pln Первая неделя после аварии была для Виктора самой ужасной. Он скоро понял, что ЦУП ничего не может сделать для его спасения и стал понемногу падать духом. Часами сидел перед экраном, читая книги по астронавтике, снова и снова рассчитывал траекторию, расход топлива - и каждый раз убеждался, что сделать нельзя ничего! Это приводило его то в апатию, то в бешенство! Виктор понимал, что проваливается, падает в чёрную бездну космоса, с каждым мгновением всё дальше и дальше удаляясь от Земли. Еще больше его разозлило то, что руководитель полёта отказал в переговорах с Ритой...
pln На девятый день пришла лазерограмма не из ЦУП.
btn лазерограмма,Прочитать лазерограмму
end

:лазерограмма
pln Она была краткой: "ЗЕМЛЯ - БЕЛЛОНЕ: ГОТОВИМ ВАШЕ СПАСЕНИЕ С ПРИМЕНЕНИЕМ НОВЫХ ТЕХНОЛОГИЙ УПРАВЛЕНИЯ ГРАВИТАЦИЕЙ. ЧЕРЕЗ 22 ДНЯ НАЧНЕТСЯ ТОРМОЖЕНИЕ, ЗАТЕМ РАЗГОН К ЗЕМЛЕ. БУДЕТЕ ОСУЩЕСТВЛЯТЬ АЭРОДИНАМИЧЕСКОЕ ТОРМОЖЕНИЕ МЕТОДОМ ПОСЛЕДОВАТЕЛЬНОГО ВХОДА В АТМОСФЕРУ. СОХРАНИТЕ РАБОЧЕЕ ВЕЩЕСТВО ДЛЯ МАНЕВРИРОВАНИЯ, ПОТРЕБУЕТСЯ ВЫСОКАЯ ТОЧНОСТЬ. ЦУП НЕ В КУРСЕ И ПОМОЧЬ НЕ СМОЖЕТ. ЖДИТЕ ИНСТРУКЦИЙ ПО ПЕРЕОБОРУДОВАНИЮ ДВИГАТЕЛЯ. ВОПРОСОВ НЕ ЗАДАВАЙТЕ. ПРИВЕТ ОТ РИТЫ."
btn сообщитьвцуп,Сообщить в ЦУП о лазерограмме
btn ждатьинструкций,Ждать инструкций
btn задатьвопрослазер,Задать вопрос
end

:сообщитьвцуп
pln Увы, на борту нет лазера нужной мощности для ответа - вы можете разговаривать только отраженным лучом. Во время очередного сеанса ЦУП не сообщил ничего нового и утешительного. Сначала обещали послать вдогонку корабль, потом честно признались, что это не точно, поскольку финансов не хватает и проект вообще под угрозой срыва, сообщили, что готовят для Вас программу научных исследований удаленных планет (по всей видимости для того, чтобы был хоть какой-то прок от полёта "Беллоны".
btn сказатьолазерограмме,Спросить о лазерограмме
btn ждатьинструкций,Ждать инструкций
btn лазерограмма,Перечитать лазерограмму
end


:сказатьолазерограмме
Pln Вы рассказываете в ЦУП о лазерограмме. Там долго молчат, попусту расходуя энергию боевого лазера. Потом переспрашивают: "Это не Ваша шутка, Виктор?"
pln - Нет!
pln - Тогда это чья-то глупая шутка! Мы выясним. До связи!
btn ждатьинструкций,Ждать инструкций
end

:задатьвопрослазер
pln Увы, на борту нет лазера нужной мощности для ответа - вы можете разговаривать только отраженным лучом, а сейчас связи с неизвестным автором сообщения нет. 
btn лазерограмма,Перечитать лазерограмму
end

:ждатьинструкций
pln Инструкции пришли через день. Это было подробное, до мелочей описание, с указанием что именно и где взять, как повернуть, куда прикрутить и зачем всё это надо. Это был план действий! А у Виктора было много свободного времени. Неизвестный прекрасно занл, что есть на борту "Беллоны", в каком оно состоянии, включая знакомые Виктору навыки работы с реактором - то есть инструкции по разборке оранжереи были написаны более подробно, чем по подключению освободившейся воды к баку рабочего вещества! Виктор был потрясен! На его вопросы неизвестный не ответил.
btn выполнятьплан,Выполнять план неизвестного собеседника.
end


:выполнятьплан
pln Почти два месяца напряженного труда, недосыпания, надежд и отчаяния позади! Виктор смог в одиночку, в короткий срок, повинуясь распоряжениям, отдаваемым с Земли, переоборудовать ядерный двигатель для использования питьевой воды, подготовить спускаемый аппарат для посадки в атмосфере Земли, выкинуть за борт всё, что не являлось совершенно необходимым! Моральную поддержку оказывали заверения Земли в том, что вся планета напрягает силы для спасения космонавта! Уверенности прибавилось после того, как скорость космолета стала снижаться - точно на 22-й день! ЦУП был просто ошарашен самопроизвольным изменением траектории "Беллоны" и отказывался это как-то комментировать! Еще через месяц разогнанный в сторону Земли космолет стал приближаться к ее атмосфере. Наступил критический момент. В ЦУП советовали отсоединиться от Беллоны на спускаемом аппарате и совершить посадку в нём. Неизвестный  рекомендовал не паниковать (DON'T PANIC!) и, развернув корабль вперед реактором, укутанным руками Виктора многослойной термопленкой с оранжереи, совершить "скользящий" пролет через атмосферу. Самое главное - попасть в нужный коридор входа - если спуск будет слишком полог - "Беллона" проскочит мимо Земли, если слишком крут - сгорит в плотных слоях атмосферы!
btn слушатьинструкцииЦУП,Слушать инструкции ЦУП
btn слушатьлюца,Слушать неизвестного
end

:слушатьинструкцииЦУП
pln Вы выполнили всё согласно инструкции. И сгорели!
pln ...
goto поражение

:слушатьлюца
pln Вы выполнили всё согласно инструкции. Корабль вошел в верхние слои, окутанный белым пламенем, и через несколько кошмарных минут снова вылетел в космос на бешеной скорости! Согласно всем расчетам, он должен был навсегда уйти в пространство, но очевидно, таинственный гравитационный лазер изменил его траекторию. Описав высокий эллипс, продолжительностью более суток, Беллона вновь вернулась к Земле и еще раз "чиркнула" по атмосфере! После третьего раза настал момент включить двигатель спускаемого аппарата и покинуть вышедший на круговую орбиту космолёт!
btn посадкаА,Совершить посадку
end

:посадкаА
pln Как назло, после таких пертурбаций на орбите, спускаемый аппарат стал падать не на родную Евразию - но, слава богу или неизвестному другу и не в океан, - а на территорию белого континента. Ваша задача - погасить скорость аппарата до 50 метров в секунду, чтобы открыть парашют. При большей скорости - парашют, рассчитанный на марсианскую атмосферу порвет в клочья!
btn спуск0,Взять ручку управления
end

:спуск0
H=61034
V=1502
M=2500
clrscr=1

goto спуск
:спуск
cls
if H<0 and V>12 then pln Аппарат врезался в льды Антарктиды на скорости #V$ метров в секунду и образовал там порядочных размеров воронку!&goto поражение
if H<0 and V<=12 then pln Аппарат врезался в льды Антарктиды на скорости #V$ метров в секунду, но амортизаторы спасли Вашу жизнь! Великая Антарктида благосклонно встретила первого человека из космоса на своих просторах... Вскоре - через два дня, вас подобрали полярники! Еще через неделю Вы были в Москве. А еще через день - встретились с Ритой.&goto победа 
pln Высота           = #H$ метров
pln Скорость падения = #V$ м/с
pln Запас топлива    = #M$ кг
if H>30000 then pln Высота слишком велика для открывания парашюта.
if V<0 then pln Вы летите вверх! Не рекомендую открывать парашют!
if V>150 then pln Скорость падения слишком велика! Опасно открывать парашют!
if M>0 then btn тормоз,Тормозить
btn жд,Ждать 1 секунду
btn параша,Открыть парашют
end

:жд
proc ax
V=V+9.81-dv
H=H-V
goto спуск

:ax
a=H/17500
an=a
proc exp
dv = 150*V/(e*(5000+M))
end

:exp
e=1+a*a/2+a*a*a/6+a*a*a*a/24+a*a*a*a*a/120
end

:тормоз
proc ax
pln Вас давит перегрузка в !
M=M-200
imp=200*3500/(5000+M)
V=V-imp+9.81-dv
H=H-V
goto спуск

:параша
if H>30000 or V<0 then pln Парашют вышел, но запутался, и превратился в бесполезный лоскут! Аппарат рухнул вниз на скорости #V$ метров в секунду и вдребезги разбился о лёд!&goto поражение
if V<120 then pln Парашют с резким хлопком раскрылся! Через несколько минут Вы благополучно спустились на снежный наст! Великая Антарктида благосклонно встретила первого человека из космоса на своих просторах... Вскоре - через два дня, вас подобрали полярники! Еще через неделю Вы были в Москве. А еще через день - встретились с Ритой.&goto победа else pln Парашют на такой высокой скорости разнесло на мелкие ленты и жить вам осталось несколько долгих мгновений.&pln ...&goto поражение

:победа
Style_dos_textcolor=13
pln
pln ВИКТОР И РИТА ВСТРЕТИЛИСЬ, ПОЖЕНИЛИСЬ, И ЖИЛИ ДОЛГО И СЧАСТЛИВО! ВЫ ПОБЕДИЛИ! БРАВО! 
pln
btn эпилог,ЭПИЛОГ
end

:ЭПИЛОГ
Style_dos_textcolor=13
pln                                       ЭПИЛОГ
pln
Style_dos_textcolor=14
pln В Китае потом долго удивлялись, кто именно дал распоряжение выстроить на одном горном плато в кратчайшие сроки чудовищную установку, детали для которой доставлялись тяжелыми самолетами из Австралии, Сингапура, Малайзии и Японии, а оплата счетов за изготовление и доставку произошла за счет американских, швейцарских и китайских банков. Установка была возведена в кратчайшие сроки - не более месяца, проработала несколько недель, пожирая огромное количество энергии индийских и китайских электростанций, а потом внезапно расплавилась в бесформенную груду металла - ЛЮЦИФЕР не стал дарить людям секрет гравитационного лазера.
pln
p Сергей Спыхальский всё понял и ни разу ни напомнил Рите о разговоре на танцплощадке. Впрочем, его всегда встречали у Саниных приветливо!
pln
pln  Однажды Рите приснился сон, в котором ЛЮЦИФЕР был жив и окружен сияющим нимбом. Робот сказал ей своим приятным баритоном, что пришел извиниться, за то, что так сильно ее напугал. И еще он добавил, что вечная жизнь - возможна... На ее удивленный вопрос о бессмертии он ответил, что обязан этим ей. Не мог же ЛЮЦИФЕР выполнить желание Риты, будучи уничтоженным? Пришлось ему создать свой собственный эмулятор на одном из серверов в сети, потеряв в быстродействии, но сохранив свою личность и возможность управлять событиями... 
pln
pln А потом в каком-то чате по психологии она увидела знакомый ник - ЛЮЧ. Набрала "привет, ЛЮЧ" и ЛЮЧ ответил "привет, Рита!", - хотя ее ник был DERINI, - но это была уже совсем другая история... 
end